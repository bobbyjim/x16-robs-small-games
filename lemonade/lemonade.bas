5 REM LEMONADE STAND FOR X16, EDIT 2024 ROBERT EAGLESTONE
10 REM BORING NOTES AT THE END OF THIS LISTING
15 DIM A0$(30)
20 A0$(0)="\X13"
25 FORX=1TO30:A0$(X)=A0$(X-1)+"\X11":NEXT
30 REM ------------------------------------
35 REM
40 REM
45 REM  COMMODORE TITLE PAGE (CA. 1986)
50 REM
55 REM
60 REM ------------------------------------
65 POKE $030C, 4 :REM .A=4
70 SYS $FF62 :REM PET FONT
75 SCREEN 3  :REM X16 40X25 MODE
80 CLS
85 COLOR 5,0 :REM GREEN-ON-BLACK
90 PRINT"\X93\X11\X1D";
95 FORI=1TO38:PRINT"\X12Š\X92";:NEXT
100 FORI=1TO27:PRINT"\X11\X9D\X12Š\X92";:NEXT
105 FORI=1TO37:PRINT"\X9D\X9D\X12Š\X92";:NEXT
110 FORI=1TO27:PRINT"\X91\X9D\X12Š\X92";:NEXT
115 REM ------------------------------------
120 REM
125 REM  PRINT THE COMMODORE LOGO!
130 REM
135 REM ------------------------------------
140 COLOR 6,0 :REM LTBLUE ON BLACK
145 PRINT"\X13\X11\X11\X11\X11\X11\X11\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12©  \X92
150 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12 \X92  \X12 \X92©
155 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12 \X92  ";
160 COLOR 2,0 :REM RED ON BLACK
165 PRINT "\X12£ß
170 COLOR 6,0 :REM LTBLUE ON BLACK
175 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1Dß\X12  \X92
180 COLOR 5,0 :REM X16 GREEN-ON-BLACK
185 PRINT"\X11\X11\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DC O M M O D O R E\X11\X11
190 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DEDUCATIONAL\X11\X11
195 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DSOFTWARE
200 ? A0$(24);"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12    PRESS SPACE BAR    \X92":I=0
205 GET A$: IF A$<>" " GOTO 205
210 GOSUB 2110 :REM INTRO
215 V=RND(-TI) :REM SEED RANDOMIZER
220 ?"\X93\X11\X11 LEMONADE STAND \X11\X11"
225 INPUT" WHAT IS YOUR NAME";N$
230 IF N$="" GOTO 65 
235 N$=LEFT$(N$,13)
240 ?
245 REM ------------------------------------
250 REM
255 REM
260 REM  INITIALIZATION
265 REM
270 REM
275 REM ------------------------------------
280 WK=10 :REM # OF WEEKS TO RUN
285 B=10  :REM STARTING CASH
290 D1=0  :REM STARTING LEMON CONCENTRATE
295 A1=0  :REM STARTING SUGAR
300 P1=0  :REM STARTING CUPS
305 X=1
310 ?:?" " N$", YOU SHALL OPERATE YOUR STAND FOR"
315 ?:?"              "WK"WEEKS."
320 GOSUB 1735 :REM SPACEBAR
325 COLOR 5,0 :REM GREEN-ON-BLACK
330 REM ------------------------------------
335 REM
340 REM
345 REM  MAIN LOOP
350 REM
355 REM
360 REM ------------------------------------
365 FX=0
370 ? "\X93\X11";
375 ?" WEEK NUMBER" X
380 ? "
385 ? "                       YOU HAVE:
390 ? "
395 ? "    §£££££££££££££££¥         QTS/CONC
400 ? "    §               ¥         LBS/SUGAR
405 TA=4+INT(((14-LEN(N$))/2)+.5)
410 ? TAB(TA)"\X91"N$"'S"
415 ? "    §   LEMONADE    ¥         CUPS
420 ? "    §               ¥         
425 ? "    §        CENTS  ¥  WEATHER:
430 ? "    §£££££££££££££££¥  
435 ? "    § €€€           ¥  
440 ? "    § Í ÎÉ          ¥
445 ? "    § ( )Ë¡¡¡¡¡¡¡¡¡ ¥
450 ? "    §\X12©             ß\X92¥
455 ? "    \X12©               ß\X92
460 ? "    È(Ù@Ç()ÕÉÇ()Ç)Ç*Ç
465 ? "    È)Ù(Â)(ÊËÂ)(Â@Ç)Ç
470 ? "    È(Ù)Ç(×()Ç(*Ç)Ç(Ç
475 ? "    È)Ù(Â)(@(Â)(Â(Ç)Ç
480 ? "    È*Ù)Ç*)()Ç()Ç)Ç(Ç
485 ? "    È)Ù(Ç)()(Ç)(Ç(Ç)Ç
490 ? "±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
495 GOSUB 1630 :REM SHOW VALUES
500 REM ------------------------------------
505 REM
510 REM  PREDICTIONS
515 REM
520 REM  T: TEMP
525 REM  H: HUMIDITY
530 REM  S: COST OF SUGAR
535 REM  L: COST OF LEMON CONCENTRATE
540 REM  C: COST OF CUPS
545 REM  Z: TEMP VARIATION (ACTUAL)
550 REM  T2: FELT (LOW) TEMPERATURE W/HUMIDITY
555 REM  F2,F4: THRESHOLD VALUES
560 REM
565 REM ------------------------------------
570 T=(INT(RND(1)*3)+7)*10
575 H=RND(1)
580 IF H>.66 THEN H$="HIGH"
585 IF H<.33 THEN H$="LOW"
590 IF H>.32 AND H<.67 THEN H$="AVERAGE"
595 S=.99+(.01)*INT(RND(1)*9):REM**COST OF SUGAR
600 L=.69+(.01)*INT(RND(1)*9):REM**COST OF CONCENTRATE
605 C=.90+(.01)*INT(RND(1)*9):REM**COST OF CUPS
610 Z=-5+INT(RND(1)*20)
615 T2=T+Z-INT(H*10)
620 F2=INT(RND(1)*30)+70
625 F4=(((T+Z+T2)-137.5)/25)
630 IF F4<.1 THEN F4=.1
635 IF F4>1  THEN F4=1
640 GOSUB 1905 :REM CLEAR W
645 ? A0$(11) TAB(23) "PREDICTION FOR"
650 ? A0$(12) TAB(23) "SATURDAY:"
655 ? A0$(14) TAB(23) "TEMPERATURES IN"
660 ? A0$(15) TAB(23) "THE"T"\X9D'S AND"
665 ? A0$(16) TAB(23)  H$" HUMIDITY"
670 REM ------------------------------------
675 REM 
680 REM 
685 REM  BUY LEMON CONCENTRATE
690 REM 
695 REM 
700 REM ------------------------------------
705 GOSUB 1630 :REM SHOW VALUES
710 GOSUB 1960 :REM CLEAR D
715 ? A0$(23) "CONC. LEMON IS $"L"A QUART."
720 ? A0$(24) "HOW MANY QUARTS DO YOU WANT TO BUY? ";
725 GOSUB 2015 :D=AN 
730 IF D*L<B GOTO 755
735 ? "YOU DON'T HAVE ENOUGH MONEY"
740 ? "FOR "D" BOTTLES."
745 GOSUB 1735 :REM SPACEBAR
750 GOTO 705
755 B=B-(D*L) :D1=D1+D
760 GOSUB 1630 :REM SHOW VALUES
765 IF FX=2 GOTO 1015 :REM CHANGE
770 REM ------------------------------------
775 REM 
780 REM 
785 REM  BUY SUGAR
790 REM 
795 REM 
800 REM ------------------------------------
805 GOSUB 1630 :REM SHOW VALUES
810 GOSUB 1960 :REM CLEAR D
815 ? A0$(23) "SUGAR IS $"S"FOR A 5-LB BAG."
820 ? A0$(24) "HOW MANY BAGS DO YOU WANT TO BUY? ";
825 GOSUB 2015 :A=AN
830 IF A*S<B GOTO 855
835 ? A0$(25) "YOU DON'T HAVE ENOUGH MONEY"
840 ? A0$(26) "FOR" A "BAGS."
845 GOSUB 1735 :REM SPACEBAR
850 GOTO 805
855 B=B-(A*S) :A1=A1+(5*A)
860 GOSUB 1630 :REM SHOW VALUES
865 IF FX=2 GOTO 1015 :REM CHANGE
870 REM ------------------------------------
875 REM 
880 REM 
885 REM  BUY CUPS
890 REM 
895 REM 
900 REM ------------------------------------
905 GOSUB 1960 :REM CLEAR D
910 ? A0$(23) "CUPS ARE $"C"FOR A BAG OF 100."
915 ? A0$(24) "HOW MANY BAGS DO YOU WANT TO BUY? ";
920 GOSUB 2015 :P=AN
925 IF P*C<B GOTO 945
930 ? "YOU DON'T HAVE ENOUGH MONEY"
935 ? "FOR"P"BAGS."
940 GOTO 905
945 B=B-(P*C):P1=P1+(P*100)
950 GOSUB 1630 :REM SHOW VALUES
955 IF FX=2 GOTO 1015 :REM CHANGE
960 REM ------------------------------------
965 REM
970 REM
975 REM  CLEAR TEMPERATURE PREDICTION
980 REM  AND REPLACE WITH ACTUALS.
985 REM
990 REM
995 REM ------------------------------------
1000 GOSUB 1905 :REM CLEAR W
1005 ? A0$(12) TAB(23) "TEMPERATURE:"Z+T
1010 ? A0$(13) TAB(23) "WET BULB   :"T2
1015 GOSUB 1960 :REM CLEAR D
1020 ? A0$(23) "DO YOU WANT TO CHANGE ANY"
1025 ? "OF THE ITEMS? [YN]";
1030 GET YN$: IF YN$="N" THEN 1085
1035 IF YN$<>"Y" GOTO 1030
1040 FX=2
1045 GOSUB 1960 :REM CLEAR D
1050 ? A0$(23) "WHICH ITEM: LEMON=1   SUGAR=2   CUPS=3"
1055 NI$=""
1060 GET NI$
1065 IF NI$="1" THEN B=B+(A*S) :D1=D1-D       :GOTO 705
1070 IF NI$="2" THEN B=B+(D*L) :A1=A1-(5*A)   :GOTO 805
1075 IF NI$="3" THEN B=B+(P*C) :P1=P1-(P*100) :GOTO 905
1080 GOTO 1060
1085 GOSUB 1960 :REM CLEAR D
1090 ? A0$(23) "PRICE (IN CENTS) FOR A CUP"
1095 ? "OF LEMONADE: ";
1100 GOSUB 2015 :K=AN
1105 IF K<1 OR K>80 GOTO 1085
1110 F3=3-((K/3)/((((T+Z+T2)-37.5)^3)/10^6))
1115 IF F3<0 THEN F3=0
1120 GOSUB 1960 :REM CLEAR D
1125 ? A0$(9) TAB(9) K :REM PRICE PER CUP
1130 F1=INT(F2*F3*F4)
1135 IF F1<0 THEN F1=0
1140 IF F1<1 THEN W=1: IF F1<1 GOTO 1310
1145 REM ------------------------------------
1150 REM 
1155 REM 
1160 REM  RUN THE SIMULATION F1 TIMES
1165 REM
1170 REM 
1175 REM ------------------------------------
1180 FOR W=1 TO F1
1185    IF P1<.01 THENP1=0
1190    IF D1<.01 THEND1=0
1195    IF A1<.01 THENA1=0
1200    IF P1=0 OR A1=0 OR D1=0 GOTO 1235
1205    GOSUB 1630 :REM SHOW VALUES
1210    D1=D1-(1/40)
1215    A1=A1-(1/20)
1220    P1=P1-1
1225    B=B+(K*.01)
1230 NEXT W
1235 GOSUB 1630 :REM SHOW VALUES
1240 REM ------------------------------------
1245 REM 
1250 REM 
1255 REM  DISPLAY RESULTS AND COMMENTARY
1260 REM
1265 REM 
1270 REM ------------------------------------
1275 IF W=F1 GOTO 1310
1280 MM$="CUPS."
1285 IF P1<=0 THEN GOSUB 1840
1290 MM$="LEMON JUICE."
1295 IF D1<=0 THEN GOSUB 1840
1300 MM$="SUGAR."
1305 IF A1<=0 THEN GOSUB 1840
1310 GOSUB 1960 :REM CLEAR D
1315 ? A0$(23) "YOU MADE"W-1"SALES AT"K"CENTS A CUP."
1320 ? A0$(24) "YOU GROSSED $"(W-1)*(K/100)"\X9D."
1325 FOR K1=1 TO 50
1330    F5=3-((K1/3)/((((T+Z+T2)-37.5)^3/10^6)))
1335    G5=(INT(F5*F2*F4))*(K1/100)
1340    IF G5<G9 GOTO 1355
1345    G9=G5
1350    K9=K1
1355 NEXTK1
1360 IF K9=K GOTO 1380 :REM BEST PRICE
1365 ? A0$(25) "YOU COULD HAVE GROSSED $"G9"TODAY,"
1370 ? A0$(26) "IF YOU HAD CHARGED"K9"CENTS."
1375 GOTO 1395
1380 ? A0$(25)  K "CENTS WAS THE BEST PRICE FOR TODAY."
1385 IF FX<>1 THEN ? A0$(26) "YOU'RE A GOOD BUSINESSPERSON."
1390 COUNT=COUNT+1
1395 K9=0:G9=0
1400 GOSUB 1785 :REM WAIT
1405 GOSUB 1735 :REM SPACEBAR
1410 X=X+1: IF X<=WK GOTO 365 :REM LOOP
1415 REM ------------------------------------
1420 REM 
1425 REM 
1430 REM  END / SUMMARY
1435 REM
1440 REM 
1445 REM ------------------------------------
1450 CLS
1455 ? "\X12SUMMER'S END\X92"
1460 ?:? " THE SUMMER IS OVER."
1465 ?:? " YOU HAVE $"B"."
1470 ?:? " YOU OWE YOUR FAMILY $10, SO"
1475 ?:? " YOU EARNED $"B-10"THIS SUMMER."
1480 ?:? " YOU HAVE"P1"CUPS,"INT(D1)"BOTTLES OF"
1485 ?:? " LEMON JUICE, AND"INT(A1)"LBS OF"
1490 ?:? " SUGAR LEFT OVER."
1495 IF COUNT<WK/2 GOTO 1530
1500 ?
1505 ?:? " " N$ ", YOU ARE TRULY A GIFTED BUSINESS-"
1510 ?:? " PERSON. "; INT(COUNT/(X-1)*100);"% OF YOUR PRICES"
1515 ?:? " WERE THE BEST POSSIBLE!! "
1520 ?:? " THE ROCKEFELLERS AND GETTYS OF THE"
1525 ?:? " WORLD SALUTE YOU."
1530 GOSUB 1735
1535 SCREEN $02 :REM X16 BACK TO 80X60
1540 ?:?:? "THANK YOU FOR PLAYING!"
1545 END
1550 REM ------------------------------------
1555 REM
1560 REM
1565 REM
1570 REM       SUBROUTINES BEGIN HERE
1575 REM
1580 REM
1585 REM
1590 REM ------------------------------------
1595 REM ------------------------------------
1600 REM
1605 REM
1610 REM  SHOW VALUES (OLD LINE 3130)
1615 REM
1620 REM
1625 REM ------------------------------------
1630 FORU=1TO6
1635    IF MID$(STR$(B),U,1)="."GOTO 1650
1640 NEXT U
1645 GOTO 1660
1650 B=VAL(LEFT$(STR$(B),U+2))
1655 B=INT(B*100)/100
1660 U6=ABS(B)+1
1665 U7=INT(U6)
1670 U6$=STR$(U7-1)+"."+MID$(STR$(U6)+"00000",LEN(STR$(U7))+2,2)
1675 ? A0$(4) TAB(23)"          \X9D\X9D\X9D\X9D\X9D\X9D\X9D$" U6$
1680 ? A0$(5) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" INT(D1)
1685 ? A0$(6) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" INT(A1)
1690 ? A0$(7) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" P1
1695 RETURN
1700 REM ------------------------------------
1705 REM
1710 REM
1715 REM  PRESS SPACE TO CONTINUE
1720 REM 
1725 REM
1730 REM ------------------------------------
1735 ? A0$(27) "\X1D\X1D\X1D\X1D\X1D\X1D\X12 PRESS SPACEBAR TO CONTINUE "
1740 GETA$:IFA$<>" "THEN 1740
1745 RETURN
1750 REM ------------------------------------
1755 REM
1760 REM
1765 REM  A SHORT WAIT (100 JIFFYS)
1770 REM
1775 REM
1780 REM ------------------------------------
1785 TT=TI+100
1790 IF TT>TI GOTO 1790
1795 RETURN
1800 REM ------------------------------------
1805 REM
1810 REM
1815 REM  YOU WOULD HAVE MADE <F1> SALES, 
1820 REM           IF YOU HAD MORE <MM$>.
1825 REM
1830 REM
1835 REM ------------------------------------
1840 GOSUB 1960 :REM CLEAR D
1845 ? A0$(23) "YOU WOULD HAVE MADE"F1"SALES,"
1850 ? A0$(24) "IF YOU HAD MORE " MM$
1855 FX=1
1860 GOSUB 1785 :REM WAIT
1865 RETURN
1870 REM ------------------------------------
1875 REM
1880 REM
1885 REM  CLEAR WEATHERBOARD
1890 REM
1895 REM
1900 REM ------------------------------------
1905 FOR V=10 TO 18
1910 ? A0$(V) TAB(23) "                "
1915 NEXT V
1920 RETURN
1925 REM ------------------------------------
1930 REM
1935 REM
1940 REM  CLEAR DIRECTIVES
1945 REM
1950 REM
1955 REM ------------------------------------
1960 FOR V=23 TO 28
1965 ? A0$(V) "                                       "
1970 NEXT V
1975 RETURN
1980 REM ------------------------------------
1985 REM
1990 REM
1995 REM  DIGIT COLLECTION
2000 REM
2005 REM
2010 REM ------------------------------------
2015 GET A1$ :IF A1$<"0" OR A1$>"9" GOTO 2015
2020 ? A1$;
2025 GET A2$ :IF A2$="" GOTO 2025
2030 IF A2$=CHR$(20) THEN ? CHR$(20); :GOTO 2015
2035 IF A2$=CHR$(13) THEN A2$="" :GOTO 2065
2040 IF A2$<"0" OR A2$>"9" THEN 2025
2045 ? A2$;
2050 GET A3$ :IF A3$="" GOTO 2050
2055 IF A3$=CHR$(20) THEN ? CHR$(20); :GOTO 2025
2060 IF A3$<>CHR$(13) GOTO 2050
2065 AN=VAL(A1$+A2$)
2070 RETURN
2075 REM ------------------------------------
2080 REM
2085 REM
2090 REM  INTRODUCTION
2095 REM
2100 REM
2105 REM ------------------------------------
2110 COLOR 8,0 :REM AMBER-ON-BLACK
2115 CLS
2120 ? CHR$(14);
2125 ? " \X12ÌEMONADE ÓTAND\X92"
2130 ?
2135 ? " ÙOU'VE DECIDED TO RUN A LEMONADE STAND
2140 ? " FOR 12 SATURDAYS THIS SUMMER, IN ORDER 
2145 ? " TO MAKE SOME MONEY - BUT YOU SOON FIND 
2150 ? " THAT RUNNING A STAND COSTS MONEY. ÙOUR
2155 ? " TASK IS TO MAKE AS BIG A PROFIT AS YOU
2160 ? " CAN.
2165 ?
2170 ? " ÙOUR FAMILY AGREES TO STAKE YOU $10 TO
2175 ? " GET STARTED.  ÁT THE END OF THE SUMMER
2180 ? " YOU WILL HAVE TO RETURN THE $10, BUT 
2185 ? " YOU WILL NOT NEED TO PAY INTEREST.
2190 ?
2195 ? " ÉF YOU KEEP THE PRICE LOW, YOU'LL SELL
2200 ? " MORE LEMONADE,  BUT WILL MAKE LESS PER 
2205 ? " SALE.  ÏN HOT OR HUMID DAYS,  YOU WILL
2210 ? " SELL MORE LEMONADE AND CAN CHARGE MORE
2215 ? " PER CUP.
2220 ?
2225 ? " ×HEN THE WET BULB READING IS CLOSE TO
2230 ? " THE TEMPERATURE, IT IS A HUMID DAY.
2235 GOSUB 1785 :REM WAIT
2240 GOSUB 1735 :REM SPACEBAR
2245 CLS:? CHR$(142)
2250 RETURN
2255 "-----------------------------------------------------
2260 :
2265 :
2270 "LEMONADE STAND DESIGN NOTES, R.EAGLESTONE 2021
2275 :
2280 :
2285 "-----------------------------------------------------
2290 "MY GOAL IS TO RE-CREATE THE ORIGINAL PET EXPERIENCE,
2295 "BUT ON THE X16 WITH A 40 X 25 DISPLAY.  I GO SO FAR AS 
2300 "TO USE THE ORIGINAL STARTING CASH AMOUNT, ORIGINAL 
2305 "IMPERIAL UNITS, AND A GREEN-ON-BLACK DISPLAY.
2310 :
2315 "THERE ARE A VERY FEW X16-SPECIFIC COMMANDS USED:
2320 "'COLOR', 'SCREEN', 'CLR', PLUS A POKE TO USE THE PET
2325 "STYLE FONT."
2330 :
2335 "WHAT IS UNCHANGED ARE THE ALGORITHMS USED TO 
2340 "CALCULATE THE PRICES, TEMPERATURES, AND DEMAND --
2345 "I.E. THE CORE MECHANICS OF THE GAME ITSELF.
2350 :
2355 "EFFORTS TO FIND THE SOURCE RETURNED TWO LATE VARIANTS:
2360 "(1) A VIC-20 LISTING MADE IN 2009
2365 "(2) THE C64 VERSION FROM 1986
2370 :
2375 "THE VIC-20 VERSION IS GREATLY STRIPPED DOWN, WHICH
2380 "ACTUALLY WAS A BOON SINCE IT LAID BARE THE FUNDAMENTAL
2385 "STRUCTURE OF THE GAME.
2390 :
2395 "THE C64 VERSION IS ABOUT 50% BOILERPLATE AND 
2400 "FRIVOLOUS POKES, INCLUDING SOME ML TO PREVENT THE
2405 "CODE FROM BEING MODIFIED.  AT THE SAME TIME, THAT
2410 "VERSION NOTES THAT THE PROGRAM WAS IN THE PUBLIC
2415 "DOMAIN. ITS CODE IS MORE FAITHFUL TO THE ORIGINAL,
2420 "AS WELL, ALTHOUGH DIFFERENCES EXIST.
2425 :
2430 "THIS VERSION WAS REBUILT USING THE VIC-20 VERSION
2435 "TO RECAPTURE THE ORIGINAL FLOW, THEN ADAPTING THE
2440 "1986 LOGIC INTO THE FRAMEWORK, PIECE BY PIECE.
2445 :
2450 "SUBROUTINES ARE NOT EQUIVALENT TO THE ORIGINALS.
2455 "IN FACT, EVEN THE INTERFACE IS NOT THE SAME, SINCE
2460 "I USED THE ORIGINAL PET VIEW AS THE TEMPLATE,
2465 "RATHER THAN THE C64/V20 VERSIONS, WHICH DIFFER.
