5 CLS
10 REM --------------------------------------
15 ? "  ÕÃÃÕÀÉÕÀÉÂ ÂÕÀÀ  ÕÃÃÕÀÉÕÃÃÕÀÀÕÀÃÀ²À "
20 ? "  Â  ÊÀËÊÀÛÊÀË«À   «À ÊÀËÂ  «À ÊÀÉ Â  "
25 ? "         ÀË   ÊÀÀ  Â        ÊÀÀÃÀË1.1 "
30 REM --------------------------------------
35 REM  INITIALIZE PLAYER
40 GOSUB 1070  :REM INIT GAME
45 GOSUB 2155  :REM INIT SOUND
50 GOSUB 1615 :REM INIT PANELS
55 REM  LINE-CLEANER
60 LI$ = "                                                           "
65 GOSUB 2265 :REM SPLASH SCREEN
70 GOSUB 515  :REM BUILD FOREST
75 R1=INT(RND(1)*50+10)
80 R2=INT(RND(1)*30+10)
85 R3  = 0
90 R4 = 0
95 R5   = 1
100 R6 = 1
105 R7  = 0
110 R6$ = "RUSTY SPORK"
115 R7$ = "TUXEDO"
120 C1    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM STR
125 C2  = C1
130 C3    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM DEX
135 C4    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM END
140 C5    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM INT
145 C0(1) = C1
150 C0(2) = C3
155 C0(3) = C4
160 C0(4) = C5
165 R8$ = "NOT YET"
170 DIM IW%(10), IA%(10) :REM WEAPONS, ARMOR
175 REM =====================================================================
180 REM
185 REM
190 REM                       DRAW THE SCREEN
195 REM
200 REM
205 REM =====================================================================
210 REM HIDE BACKGROUND
215 COLOR 1,6 :CLS :REM WHITE ON BLUE
220 GOSUB 2095 
225 L= 1  :T= 44 :W= 76  :H= 15  :L$= "" :GOSUB 2025
230 GOSUB 1845 :REM DRAW PANELS
235 GOSUB 425
240 REM  ----------------------------------
245 REM  PRINT PLAYER AND POSITION CURSOR
250 REM  ----------------------------------
255 LOCATE R2-3
260 COLOR 1,0 :REM TRANSPARENT (CLEARS FOG)
265 ? TAB(R1-4) C9$;
270 COLOR 1,6 :REM BLUE UNDER WHITE
275 ? L5$ TAB(R1) "@"
280 LOCATE 4
285 ? LI$ : ? "\X91   STR" C1 "SCORE:" R3 "HUNGER:" R4
290 IF RND(1)>0.95 THEN R4 = R4 + 1
295 T0 = T0 - 1
300 IF T0 = 0 THEN ? VP$(5) LI$
305 GET A$ :IF A$="" GOTO 305
310 XX=0 :YY=0
315 IF A$="\X11" THEN YY=+1 :REM DOWN
320 IF A$="\X91" THEN YY=-1 :REM UP
325 IF A$="\X1D" THEN XX=+1 :REM RIGHT
330 IF A$="\X9D" THEN XX=-1 :REM LEFT
335 IF A$="E" THEN GOSUB 465
340 IF (A$="." OR A$=" ") AND C1 < C2 THEN C1 = C1 + 1
345 LO = VPEEK(1,(R2+YY)*$100 + (R1+XX)*2)
350 IF LO < $20 THEN NN=LO :GOSUB 1470 :GOTO 365
355 IF LO > $22 AND LO < $2E THEN :GOSUB 2460 :GOSUB 425
360 IF LO < $41 THEN R1=R1+XX :R2=R2+YY :HU=HU+1: LO = VPEEK(1,R2*$100 + R1*2)
365 FOR NN=0 TO 31
370    AA=MA(NN)
375    XX=XM(NN)-R1
380    YY=YM(NN)-R2
385    IF ABS(XX)+ABS(YY) <= 1 THEN GOSUB 1395 :GOTO 400
390    IF RND(1)>0.5 THEN 400
395    IF ABS(XX) < 9 AND ABS(YY) < 9 THEN GOSUB 1285
400 NEXT
405 IF C1 < 1 GOTO 415
410 IF (R1<70) AND (R1>7) AND (R2<42) AND (R2>5) GOTO 255
415 GOSUB 945
420 END
425 LOCATE 46
430 ? SPC(6) LI$: ? "\X91" SPC(6) "WEAPON: " R6$ " (" R6 ")"
435 ? SPC(6) LI$: ? "\X91" SPC(6) "ARMOR : " R7$ " (" R7 ")"
440 ? SPC(6) LI$: ? "\X91" SPC(6) "FOOD  : " R5
445 ? SPC(6) LI$: ? "\X91" SPC(6) "AMULET: " R8$ 
450 ? SPC(6) LI$: ? "\X91" SPC(6) "SCORE : " R3
455 ?
460 RETURN
465 IF R5 < 1 THEN RETURN
470 IF R4 < 1 THEN RETURN
475 R5 = R5 - 1
480 R4 = R4 - 1
485 T0 = 5
490 LOCATE 5
495 ? LI$ :?"\X91   YOU FEEL BETTER."
500 GOSUB 425
505 RETURN
510 REM                         BUILD THE FOREST
515 REM  SET UP VIDEO REGISTERS
520 POKE $9F2D, %01100000 :REM MAP HEIGHT=1, WIDTH=2 = 64X128 TILES
525 POKE $9F2E, %10000000 :REM MAP BASE ADDR = 128 X512 = $10000.
530 POKE $9F2F, %11111000 :REM TILE BASE ADDR = $1F000.
535 REM ALSO, TILE HT=0, WD=0, SO 8 PIXELS X 8 PIXELS
540 REM  NO SCROLLING, PLEASE ($9F30-$9F33)
545 POKE $9F30, 0
550 POKE $9F31, 0
555 POKE $9F32, 0
560 POKE $9F33, 0
565 COLOR 1,0 :CLS  :REM 0=TRANSPARENT
570 POKE $9F29, %00110001 :REM $31=LAYERS 1,0. OUTPUT MODE=1 (VGA)
575 CC=4:CD=45
580 IF VPEEK(1,0) = 32 THEN CC=8 :CD=40
585 REM  UNROLLING THIS LOOP SHAVES 5 SECS
590 CL=$05  :REM BG/FG COLOR NYBBLES ($15=WHITE UNDER GREEN)
595 REM MONSTER DATA
600 MN=0
605 DIM MA(31)
610 DIM XM(31),YM(31)
615 DIM SO(31) :REM TERRAIN THE MONSTER IS .S.TANDING .O.N
620 POKE $9F25, PEEK($9F25) AND 254 : REM SELECT PORT 0
625 POKE $9F22, %00010001 : REM INCR 1 AND VRAM ADDR BIT 16
630 FOR Y=CC TO CD
635    Y0=Y*$100
640    FOR X=0 TO 158 STEP 8
645       P=Y0+X
650       POKE $9F21, INT(P/256) AND 255 :REM VRAM 15:8
655       POKE $9F20, P AND 255          :REM VRAM 7:0
660       IF Y<8 OR Y>40 GOTO 775
665       IF X<14 OR X>140 GOTO 775
670          FF=3
675          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
680          POKE $9F23, FO(FF) :REM CHARACTER INDEX
685          POKE $9F23, CL   :REM BG/FG COLOR NYBBLES
690          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
695          POKE $9F23, FO(FF)
700          POKE $9F23, CL
705          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
710          POKE $9F23, FO(FF)
715          POKE $9F23, CL
720          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
725          POKE $9F23, FO(FF)
730          POKE $9F23, CL
735          IF Y<9 OR Y>39 GOTO 815
740          REM TREASURE
745          IF RND(1) > 0.08 GOTO 815
750             XX = INT(RND(4))
755             VPOKE 1,P+2*XX,FO(5+INT(RND(1)*4))
760             POKE $9F25, PEEK($9F25) AND 254 : REM RE-SELECT PORT 0
765             POKE $9F22, %00010001 : REM INCR 1 AND VRAM ADDR BIT 16
770             GOTO 815
775             POKE $9F23, $20
780             POKE $9F23, CL
785             POKE $9F23, $20
790             POKE $9F23, CL
795             POKE $9F23, $20
800             POKE $9F23, CL
805             POKE $9F23, $20
810             POKE $9F23, CL
815    NEXT
820 NEXT
825 REM  PLACE MONSTERS
830 FOR M = 1 TO 26 :REM 0 TO 31
835    X = 10 + INT(RND(1)*60)
840    Y = 10 + INT(RND(1)*30)
845    XX = ABS(X-R1)
850    YY = ABS(Y-R2)
855    IF XX<6 AND YY<6 GOTO 835 :REM TOO CLOSE!
860    MA = Y * $100 + X * 2
865    MA(M) = MA
870    XM(M)=X
875    YM(M)=Y
880    SO(M) = VPEEK(1,MA)
885    VPOKE 1,MA,M
890 NEXT
895 REM AMULET OF YENDOR
900 VPOKE 1, INT(RND(1)*30+10) * $100 + INT(RND(1)*60+10) * 2, FO(10)
905 RETURN
910 REM ===================================
915 REM
920 REM
925 REM     GAME OVER - PRINT SCORE
930 REM
935 REM
940 REM ===================================
945 COLOR 1,$06 :REM WHITE ON BLUE
950 CLS
955 GOSUB 2095
960 L= 1  :T= 1  :W= 76  :H= 17  :L$= "GAME OVER"   :GOSUB 2025
965 GOSUB 1845
970 LOCATE 3,6
975 ? "YOUR ACCOMPLISHMENTS"
980 LOCATE 6,6
985 ? "ESCAPED THE FOREST: ";
990 IF C1 < 1 THEN ? "NO"
995 IF C1 > 0 THEN ? "YES"
1000 LOCATE 9,6
1005 ? "FOUND THE AMULET:   ";
1010 IF R8 = 0 THEN ? "NO"
1015 IF R8 = 1 THEN ? "YES"
1020 LOCATE 12,6
1025 ? "SCORE:             " R3
1030 GOSUB 2210
1035 LOCATE 15,6
1040 ? "THANK YOU FOR PLAYING ROGUE FOREST!"
1045 LOCATE 18
1050 RETURN
1055 REM  ---------------------------------
1060 REM  INITIALIZE 
1065 REM  ---------------------------------
1070 L5$ = "\X91\X91\X91\X91\X91" :REM LEFT 5
1075 C9$ = "         \X11\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1080 C9$ = C9$+C9$+C9$+C9$+C9$+C9$+C9$+C9$+C9$
1085 DIM M$(31)
1090 FOR X=0 TO 31 :READ M$(X) :NEXT
1095 DATA  DOPPLEGANGER,  ABALONE,  BASILISK,    CENTAUR,    DRAGON
1100 DATA  ETTIN,         FUZZY,    GHOUL,       HELLHOUND,  IMP
1105 DATA  JINN,          KOBOLD,   LEPRECHAUN,  MINOTAUR,   NYMPH
1110 DATA  ORC,           PYTHON,   QUAGGA,      RATTLER,    SKELETON
1115 DATA  TROLL,         UR-VILE,  VAMPIRE,     WRAITH,     XEROC
1120 DATA  YETI,          ZOMBIE,   WYVERN,      MANTICORE,  VELOCIRAPTOR
1125 DATA  SPHINX,        SPIDER
1130 FOR X=0 TO 7 :READ N$(X) :NEXT
1135 DATA CHENEY, DURIN, FARGOL, KHAALO, REJNALDI, SARA, SHARIK, ZOG
1140 FOR X=0 TO 7 :READ C$(X) :NEXT
1145 DATA "","RED ","GREEN ","BLUE ","TIN ","WHITE ","PALE ","GOLD "
1150 FOR X=0 TO 7 :READ D$(X) :NEXT
1155 DATA ""," OF WAR"," OF AGILITY"," OF PAIN"," OF CUNNING"
1160 DATA " OF SKILL",""," OF MAGIC"
1165 FOR X=0 TO 7 :READ W$(X) :NEXT
1170 DATA DAGGER, STAFF, HAMMER, MACHETE, AXE, SWORD, LABRYS, HALBERD
1175 FOR X=0 TO 7 :READ A$(X) :NEXT
1180 DATA LEATHER, HAUBERK, RINGMAIL, MESH, MAIL, SPLINT, PLATE, MITHRIL
1185 REM FOREST ELEMENTS
1190 FO(0) = $41 :REM TREE
1195 FO(1) = $2E :REM DOT
1200 FO(2) = $2E :REM DOT
1205 FO(3) = $43 :REM HORIZ LINE
1210 FO(4) = $42 :REM VERT LINE
1215 FO(5) = $23 :REM ARMOR (#)
1220 FO(6) = $24 :REM FOOD ($)
1225 FO(7) = $27 :REM WEAPON (')
1230 FO(8) = $27
1235 FO(9) = $24
1240 FO(10) = $2A :REM AMULET OF YENDOR
1245 RETURN
1250 REM =================================================================
1255 REM
1260 REM
1265 REM       MOVE MONSTER:NN ADDRESS:AA RELATIVE_POS:XX,YY
1270 REM
1275 REM
1280 REM =================================================================
1285 VPOKE 1,AA,SO(NN)  :REM REDRAW TILE
1290 DX=0 :DY=0
1295 IF RND(1)>0.5 GOTO 1310
1300 IF XX<0 THEN AA=AA+2 :DX=1 :GOTO 1320
1305 IF XX>0 THEN AA=AA-2 :DX=-1 :GOTO 1320
1310 IF YY<0 THEN AA=AA+$100 :DY=1 :GOTO 1320
1315 IF YY>0 THEN AA=AA-$100 :DY=-1 :GOTO 1320
1320 V=VPEEK(1,AA)
1325 IF V < $20 OR V = $2A GOTO 1355
1330 SO(NN)=VPEEK(1,AA)
1335 MA(NN)=AA
1340 XM(NN)=XM(NN)+DX
1345 YM(NN)=YM(NN)+DY
1350 VPOKE 1,AA,NN
1355 RETURN
1360 REM =================================================================
1365 REM
1370 REM
1375 REM                  MONSTER NN ATTACKS
1380 REM
1385 REM
1390 REM =================================================================
1395 LOCATE 51, 6
1400 ? LI$ : ? "\X91";
1405 W = INT(RND(1)*4)
1410 IF INT(RND(1)*40) < R7 THEN W=0
1415 IF W=0 THEN ? SPC(6) "THE " M$(NN) " MISSES."
1420 IF W>0 THEN ? SPC(6) "THE " M$(NN) " HITS YOU FOR" W "POINTS." :GOSUB 2185
1425 C1 = C1 - W
1430 RETURN
1435 REM =================================================================
1440 REM
1445 REM
1450 REM                 ATTACK MONSTER NN
1455 REM
1460 REM
1465 REM =================================================================
1470 LOCATE 52, 6
1475 ? LI$ : ? "\X91";
1480 T1 = R6 AND 7
1485 T2 = (R6/8) AND 7
1490 T3 = (R6/64) AND 7
1495 T4 = (R6/256) AND 7
1500 T5 = T1+T2+T3+T4+1 - R4
1505 IF T5 < 2 THEN T5 = 2
1510 W = INT(RND(1)*T5)
1515 HU=HU+1
1520 N=RND(1) * NN
1525 ? SPC(6) "YOU HIT THE " M$(NN) " FOR" W "POINTS"; :GOSUB 2185
1530 IF W > N THEN ? ", KILLING IT";: VPOKE 1,MA(NN),$2E :YM(NN)=-1000 :R3=R3+W
1535 ? "."
1540 RETURN
1545 REM -----------------------------------------
1550 REM
1555 REM  PANEL UTILITY 
1560 REM
1565 REM -----------------------------------------
1570 REM
1575 REM  NIFTY DISPLAY PANELS
1580 REM
1585 REM 
1590 REM ÕÃÃÃÃÃÃÃÉ
1595 REM Â       Â
1600 REM Â       Â
1605 REM ÊÃÃÃÃÃÃÃË
1610 REM 
1615 REM  ------------------------------------
1620 REM  SET UP PANEL ARRAYS BASED ON MAX
1625 REM  NUMBER OF PANELS (N=6)
1630 REM  ------------------------------------
1635 N = 6
1640 P7 = 0
1645 DIM P0$(N)
1650 DIM P1(N), P2(N), P3(N), P4(N)
1655 DIM P5$(N,30), P6(N)
1660 REM  -----------------------------------
1665 REM  HORIZONTAL BAR ARRAY
1670 REM  -----------------------------------
1675 DIM HB$(80)
1680 HB$(0) = "\XC3"
1685 FOR V=1 TO 80
1690    HB$(V) = HB$(V-1) + "\XC3"
1695 NEXT
1700 REM  -----------------------------------
1705 REM  VERTICAL BAR ARRAY
1710 REM  -----------------------------------
1715 DIM VB$(30)
1720 VB$(0) = "\XC2\X11\X9D"
1725 FOR V=1 TO 30
1730    VB$(V) = VB$(V-1) + "\XC2\X11\X9D"
1735 NEXT
1740 REM  ----------------------------------
1745 REM  PANEL EDGES
1750 REM  ----------------------------------
1755 BA$ = "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1760 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1765 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1770 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1775 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1780 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1785 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1790 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1795 SP$ = "                    "
1800 SP$ = SP$ + "                    "
1805 SP$ = SP$ + "                    "
1810 SP$ = SP$ + "                    "
1815 RETURN
1820 REM ------------------------------------
1825 REM
1830 REM DRAW PANELS FROM VARIABLE DATA
1835 REM
1840 REM ------------------------------------
1845 FOR N = 0 TO P7 - 1
1850    LOCATE P1(N), P2(N)
1855    ? "\XD5\XC3" P0$(N) "\XC3";
1860    ? HB$(P3(N) - 3 - LEN(P0$(N)));
1865    ? "\XC9\X11";
1870    B2$ = LEFT$(BA$, P3(N)+2)
1875    S2$ = LEFT$(SP$, P3(N))
1880    FOR RR=3 TO P4(N)
1885    REM DON'T FORGET THE LINES !!
1890    ? B2$ "\XC2" S2$ "\XC2\X11";
1895    NEXT
1900    ? B2$ "\XCA";
1905    ? HB$(P3(N)-1);
1910    ? "\XCB";
1915 NEXT
1920 RETURN
1925 REM ------------------------------------
1930 REM
1935 REM SHOW THE PANEL TEXTS
1940 REM
1945 REM ------------------------------------
1950 FOR N = 0 TO P7 - 1
1955    LOCATE P1(N)
1960    ?
1965    FOR X = 0 TO P4(N)-3
1970       ? SPC(P2(N)+2) P5$(N,X)
1975    NEXT
1980 NEXT
1985 RETURN
1990 REM ------------------------------------
1995 REM
2000 REM ADD A PANEL TO THE LIST!
2005 REM T: TOP, L: LEFT, W: WIDTH, H:HEIGHT
2010 REM L$: LABEL
2015 REM
2020 REM ------------------------------------
2025 N = P7
2030 P0$(N) = L$
2035 P1(N) = T
2040 P2(N) = L
2045 P3(N) = W
2050 P4(N) = H
2055 P6(N) = -1
2060 P7 = N + 1
2065 RETURN
2070 REM ------------------------------------------------------
2075 REM
2080 REM "DELETE" ALL PANELS
2085 REM
2090 REM ------------------------------------------------------
2095 P7 = 0
2100 RETURN
2105 REM ------------------------------------------------------
2110 REM
2115 REM SET CONTENTS OF A PANEL
2120 REM N:PANEL NUM, BU$(): LINES, LC: LINE COUNT
2125 REM
2130 REM ------------------------------------------------------
2135 FOR X = 0 TO LC-1
2140    P5$(N,X) = BU$(X)
2145 NEXT
2150 RETURN
2155 IF PEEK($400)=0 THEN LOAD "RF-EFFECTS.PRG",8,1,$0400
2160 RETURN
2165 SYS $0400
2170 T = TI 
2175 IF TI < T + 100 GOTO 2175
2180 RETURN
2185 SYS $0403
2190 T = TI 
2195 IF TI < T + 100 GOTO 2195
2200 RETURN
2205 RETURN
2210 SYS $0409
2215 T = TI
2220 IF TI < T + 300 GOTO 2220
2225 RETURN
2230 REM =================================================================
2235 REM
2240 REM
2245 REM                       "SPLASH" SCREEN
2250 REM
2255 REM
2260 REM =================================================================
2265 L= 1  :T= 5  :W= 76  :H= 13  :L$= "WELCOME"     :GOSUB 2025
2270 L= 1  :T= 19 :W= 76  :H= 15  :L$= "MAP KEY"     :GOSUB 2025
2275 L= 1  :T= 35 :W= 76  :H= 12  :L$= "COMMAND KEY" :GOSUB 2025
2280 GOSUB 1845 :REM DRAW PANELS
2285 LOCATE 8
2290 ? SPC(6) "FIND THE AMULET OF YENDOR AND EXIT THE FOREST ALIVE!"
2295 ?
2300 ? SPC(6) "POINTS ARE AWARDED FOR ITEMS COLLECTED AND MONSTERS KILLED."
2305 LOCATE 21
2310 ? SPC(6) "@ ... YOU"
2315 ? SPC(6) CHR$(FO(0)) " ... TREE (IMPASSABLE BY YOU)"
2320 ? SPC(6) CHR$(FO(1)) " ... CLEARING"
2325 ? SPC(6) CHR$(FO(5)) " ... ARMOR"
2330 ? SPC(6) CHR$(FO(6)) " ... FOOD"
2335 ? SPC(6) CHR$(FO(7)) " ... WEAPON"
2340 ? SPC(6) CHR$(FO(10))" ... AMULET OF YENDOR"
2345 ?:?
2350 ? SPC(6) "-- ANYTHING THAT MOVES IS A MONSTER --"
2355 LOCATE 37
2360 ? SPC(6) "? ............ HELP"
2365 ? SPC(6) "CURSOR KEYS .. MOVE / ATTACK"
2370 ? SPC(6) ". ............ REST"
2375 ? SPC(6) "E ............ EAT FOOD"
2380 ? SPC(6) "X ............ EXIT"
2385 ?
2390 LOCATE 50
2395 ? "   GOOD LUCK!"
2400 ?
2405 GOSUB 2210
2410 INPUT "   PLEASE TYPE YOUR NAME"; R0$
2415 X=RND(-TI)
2420 RETURN
2425 REM =================================================================
2430 REM
2435 REM
2440 REM                   TREASURE FINDS (TYPE IN 'LO')
2445 REM
2450 REM
2455 REM =================================================================
2460 COLOR 1,6
2465 REM CLEAR THE SQUARE THE PLAYER IS *LOOKING AT*
2470 VPOKE 1,(R2+YY)*$100 + (R1+XX)*2, $2E
2475 GOSUB 2165
2480 REM  ----------------------
2485 REM  PRE-CALC SOME VALUES
2490 REM  ----------------------
2495 T1=INT(RND(1)*8)
2500 T2=INT(RND(1)*8)
2505 T3=INT(RND(1)*8)
2510 T4=INT(RND(1)*8)
2515 REM  ------------------------
2520 REM  FIGURE OUT WHAT WE GOT
2525 REM  ------------------------
2530 IF LO=FO(10) GOTO 2555
2535 IF LO=FO(5)  GOTO 2615
2540 IF LO=FO(6)  GOTO 2585
2545 IF LO=FO(7)  GOTO 2670
2550 RETURN
2555 LOCATE 5
2560 ? LI$ :? "\X91   YOU FOUND THE AMULET OF YENDOR! "
2565 R8$ = "FOUND IT!"
2570 R8=1  :REM PLAYER-YENDOR
2575 T0 = 5
2580 RETURN
2585 T1 = INT(RND(1)*8+1)
2590 LOCATE 5
2595 ? LI$ :?"\X91   YOU FOUND" T1 "FOOD."
2600 R5=R5 + T1
2605 T0 = 5
2610 RETURN
2615 R3 = R3 + T1+T2+T3+T4
2620 AD$ = N$(T1) + "'S " + C$(T2) + A$(T3) + D$(T4)
2625 GOSUB 2725
2630 ? SPC(6) "YOU FOUND " AD$
2635 ?:? SPC(6) "REPLACE YOUR CURRENT ARMOR WITH IT? [YN] "; 
2640 GET YN$ :IF YN$<> "Y" AND YN$ <> "N" GOTO 2640
2645 GOSUB 2755
2650 IF YN$ = "N" THEN RETURN
2655 R7 = T1 + T2 +T3 + T4 
2660 R7$ = AD$
2665 RETURN
2670 R3 = R3 + T1+T2+T3+T4
2675 WD$ = N$(T1) + "'S " + C$(T2) + W$(T3) + D$(T4)
2680 GOSUB 2725
2685 ? SPC(6) "YOU FOUND " WD$
2690 ?:? SPC(6) "REPLACE YOUR CURRENT WEAPON WITH IT? [YN] "; 
2695 GET YN$ :IF YN$<> "Y" AND YN$ <> "N" GOTO 2695
2700 GOSUB 2755
2705 IF YN$ = "N" THEN RETURN
2710 R6 = T1 + T2 * 8 + T3 * 64 + T4 * 256
2715 R6$ = WD$
2720 RETURN
2725 LOCATE 51
2730 ?SPC(6) LI$
2735 ?SPC(6) LI$
2740 ?SPC(6) LI$
2745 ?"\X91\X91\X91";
2750 RETURN
2755 ? "\X91\X91\X91\X91"
2760 ?SPC(6) LI$ 
2765 ?SPC(6) LI$ 
2770 ?SPC(6) LI$ 
2775 ?SPC(6) LI$
2780 RETURN
