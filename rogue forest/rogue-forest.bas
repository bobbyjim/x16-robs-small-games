5 CLS
10 REM --------------------------------------
15 ? "  ÕÃÃÕÀÉÕÀÉÂ ÂÕÀÀ  ÕÃÃÕÀÉÕÃÃÕÀÀÕÀÃÀ²À "
20 ? "  Â  ÊÀËÊÀÛÊÀË«À   «À ÊÀËÂ  «À ÊÀÉ Â  "
25 ? "         ÀË   ÊÀÀ  Â        ÊÀÀÃÀË1.5 "
30 REM --------------------------------------
35 REM  INITIALIZE PLAYER
40 GOSUB 1065  :REM INIT GAME
45 GOSUB 2150  :REM INIT SOUND
50 GOSUB 1610 :REM INIT PANELS
55 REM  LINE-CLEANER
60 LI$ = "                                                           "
65 GOSUB 2260 :REM SPLASH SCREEN
70 GOSUB 510  :REM BUILD FOREST
75 R1=INT(RND(1)*50+10)
80 R2=INT(RND(1)*30+10)
85 R3  = 0
90 R4 = 0
95 R5   = 1
100 R6 = 1
105 R7  = 0
110 R6$ = "RUSTY SPORK"
115 R7$ = "TUXEDO"
120 C1    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM STR
125 C2  = C1
130 C3    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM DEX
135 C4    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM END
140 C5    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM INT
145 C0(1) = C1
150 C0(2) = C3
155 C0(3) = C4
160 C0(4) = C5
165 R8$ = "NOT YET"
170 REM =====================================================================
175 REM
180 REM
185 REM                       DRAW THE SCREEN
190 REM
195 REM
200 REM =====================================================================
205 REM HIDE BACKGROUND
210 COLOR 1,6 :CLS :REM WHITE ON BLUE
215 GOSUB 2090 
220 L= 1  :T= 44 :W= 76  :H= 15  :L$= "" :GOSUB 2020
225 GOSUB 1840 :REM DRAW PANELS
230 GOSUB 420
235 REM  ----------------------------------
240 REM  PRINT PLAYER AND POSITION CURSOR
245 REM  ----------------------------------
250 LOCATE R2-3
255 COLOR 1,0 :REM TRANSPARENT (CLEARS FOG)
260 ? TAB(R1-4) C9$;
265 COLOR 1,6 :REM BLUE UNDER WHITE
270 ? L5$ TAB(R1) "@"
275 LOCATE 4
280 ? LI$ : ? "\X91   STR" C1 "SCORE:" R3 "HUNGER:" R4
285 IF RND(1)>0.95 THEN R4 = R4 + 1
290 T0 = T0 - 1
295 IF T0 = 0 THEN ? VP$(5) LI$
300 GET A$ :IF A$="" GOTO 300
305 XX=0 :YY=0
310 IF A$="\X11" THEN YY=+1 :REM DOWN
315 IF A$="\X91" THEN YY=-1 :REM UP
320 IF A$="\X1D" THEN XX=+1 :REM RIGHT
325 IF A$="\X9D" THEN XX=-1 :REM LEFT
330 IF A$="E" THEN GOSUB 460
335 IF (A$="." OR A$=" ") AND C1 < C2 THEN C1 = C1 + 1
340 LO = VPEEK(1,(R2+YY)*$100 + (R1+XX)*2)
345 IF LO < $20 THEN NN=LO :GOSUB 1465 :GOTO 360
350 IF LO > $22 AND LO < $2E THEN :GOSUB 2455 :GOSUB 420
355 IF LO < $41 THEN R1=R1+XX :R2=R2+YY :HU=HU+1: LO = VPEEK(1,R2*$100 + R1*2)
360 FOR NN=0 TO 31
365    AA=MA(NN)
370    XX=XM(NN)-R1
375    YY=YM(NN)-R2
380    IF ABS(XX)+ABS(YY) <= 1 THEN GOSUB 1390 :GOTO 395
385    IF RND(1)>0.5 THEN 395
390    IF ABS(XX) < 9 AND ABS(YY) < 9 THEN GOSUB 1280
395 NEXT
400 IF C1 < 1 GOTO 410
405 IF (R1<70) AND (R1>7) AND (R2<42) AND (R2>5) GOTO 250
410 GOSUB 940
415 END
420 LOCATE 46
425 ? SPC(6) LI$: ? "\X91" SPC(6) "WEAPON: " R6$ " (" R6 ")"
430 ? SPC(6) LI$: ? "\X91" SPC(6) "ARMOR : " R7$ " (" R7 ")"
435 ? SPC(6) LI$: ? "\X91" SPC(6) "FOOD  : " R5
440 ? SPC(6) LI$: ? "\X91" SPC(6) "AMULET: " R8$ 
445 ? SPC(6) LI$: ? "\X91" SPC(6) "SCORE : " R3
450 ?
455 RETURN
460 IF R5 < 1 THEN RETURN
465 IF R4 < 1 THEN RETURN
470 R5 = R5 - 1
475 R4 = R4 - 1
480 T0 = 5
485 LOCATE 5
490 ? LI$ :?"\X91   YOU FEEL BETTER."
495 GOSUB 420
500 RETURN
505 REM                         BUILD THE FOREST
510 REM  SET UP VIDEO REGISTERS
515 POKE $9F2D, %01100000 :REM MAP HEIGHT=1, WIDTH=2 = 64X128 TILES
520 POKE $9F2E, %10000000 :REM MAP BASE ADDR = 128 X512 = $10000.
525 POKE $9F2F, %11111000 :REM TILE BASE ADDR = $1F000.
530 REM ALSO, TILE HT=0, WD=0, SO 8 PIXELS X 8 PIXELS
535 REM  NO SCROLLING, PLEASE ($9F30-$9F33)
540 POKE $9F30, 0
545 POKE $9F31, 0
550 POKE $9F32, 0
555 POKE $9F33, 0
560 COLOR 1,0 :CLS  :REM 0=TRANSPARENT
565 POKE $9F29, %00110001 :REM $31=LAYERS 1,0. OUTPUT MODE=1 (VGA)
570 CC=4:CD=45
575 IF VPEEK(1,0) = 32 THEN CC=8 :CD=40
580 REM  UNROLLING THIS LOOP SHAVES 5 SECS
585 CL=$05  :REM BG/FG COLOR NYBBLES ($15=WHITE UNDER GREEN)
590 REM MONSTER DATA
595 MN=0
600 DIM MA(31)
605 DIM XM(31),YM(31)
610 DIM SO(31) :REM TERRAIN THE MONSTER IS .S.TANDING .O.N
615 POKE $9F25, PEEK($9F25) AND 254 : REM SELECT PORT 0
620 POKE $9F22, %00010001 : REM INCR 1 AND VRAM ADDR BIT 16
625 FOR Y=CC TO CD
630    Y0=Y*$100
635    FOR X=0 TO 158 STEP 8
640       P=Y0+X
645       POKE $9F21, INT(P/256) AND 255 :REM VRAM 15:8
650       POKE $9F20, P AND 255          :REM VRAM 7:0
655       IF Y<8 OR Y>40 GOTO 770
660       IF X<14 OR X>140 GOTO 770
665          FF=3
670          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
675          POKE $9F23, FO(FF) :REM CHARACTER INDEX
680          POKE $9F23, CL   :REM BG/FG COLOR NYBBLES
685          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
690          POKE $9F23, FO(FF)
695          POKE $9F23, CL
700          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
705          POKE $9F23, FO(FF)
710          POKE $9F23, CL
715          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
720          POKE $9F23, FO(FF)
725          POKE $9F23, CL
730          IF Y<9 OR Y>39 GOTO 810
735          REM TREASURE
740          IF RND(1) > 0.08 GOTO 810
745             XX = INT(RND(4))
750             VPOKE 1,P+2*XX,FO(5+INT(RND(1)*4))
755             POKE $9F25, PEEK($9F25) AND 254 : REM RE-SELECT PORT 0
760             POKE $9F22, %00010001 : REM INCR 1 AND VRAM ADDR BIT 16
765             GOTO 810
770             POKE $9F23, $20
775             POKE $9F23, CL
780             POKE $9F23, $20
785             POKE $9F23, CL
790             POKE $9F23, $20
795             POKE $9F23, CL
800             POKE $9F23, $20
805             POKE $9F23, CL
810    NEXT
815 NEXT
820 REM  PLACE MONSTERS
825 FOR M = 1 TO 26 :REM 0 TO 31
830    X = 10 + INT(RND(1)*60)
835    Y = 10 + INT(RND(1)*30)
840    XX = ABS(X-R1)
845    YY = ABS(Y-R2)
850    IF XX<6 AND YY<6 GOTO 830 :REM TOO CLOSE!
855    MA = Y * $100 + X * 2
860    MA(M) = MA
865    XM(M)=X
870    YM(M)=Y
875    SO(M) = VPEEK(1,MA)
880    VPOKE 1,MA,M
885 NEXT
890 REM AMULET OF YENDOR
895 VPOKE 1, INT(RND(1)*30+10) * $100 + INT(RND(1)*60+10) * 2, FO(10)
900 RETURN
905 REM ===================================
910 REM
915 REM
920 REM     GAME OVER - PRINT SCORE
925 REM
930 REM
935 REM ===================================
940 COLOR 1,$06 :REM WHITE ON BLUE
945 CLS
950 GOSUB 2090
955 L= 1  :T= 1  :W= 76  :H= 17  :L$= "GAME OVER"   :GOSUB 2020
960 GOSUB 1840
965 LOCATE 3,6
970 ? "YOUR ACCOMPLISHMENTS"
975 LOCATE 6,6
980 ? "ESCAPED THE FOREST: ";
985 IF C1 < 1 THEN ? "NO"
990 IF C1 > 0 THEN ? "YES"
995 LOCATE 9,6
1000 ? "FOUND THE AMULET:   ";
1005 IF R8 = 0 THEN ? "NO"
1010 IF R8 = 1 THEN ? "YES"
1015 LOCATE 12,6
1020 ? "SCORE:             " R3
1025 GOSUB 2205
1030 LOCATE 15,6
1035 ? "THANK YOU FOR PLAYING ROGUE FOREST!"
1040 LOCATE 18
1045 RETURN
1050 REM  ---------------------------------
1055 REM  INITIALIZE 
1060 REM  ---------------------------------
1065 L5$ = "\X91\X91\X91\X91\X91" :REM LEFT 5
1070 C9$ = "         \X11\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1075 C9$ = C9$+C9$+C9$+C9$+C9$+C9$+C9$+C9$+C9$
1080 DIM M$(31)
1085 FOR X=0 TO 31 :READ M$(X) :NEXT
1090 DATA  DOPPLEGANGER,  ABALONE,  BASILISK,    CENTAUR,    DRAGON
1095 DATA  ETTIN,         FUZZY,    GHOUL,       HELLHOUND,  IMP
1100 DATA  JINN,          KOBOLD,   LEPRECHAUN,  MINOTAUR,   NYMPH
1105 DATA  ORC,           PYTHON,   QUAGGA,      RATTLER,    SKELETON
1110 DATA  TROLL,         UR-VILE,  VAMPIRE,     WRAITH,     XEROC
1115 DATA  YETI,          ZOMBIE,   WYVERN,      MANTICORE,  VELOCIRAPTOR
1120 DATA  SPHINX,        SPIDER
1125 FOR X=0 TO 7 :READ N$(X) :NEXT
1130 DATA CHENEY, DURIN, FARGOL, KHAALO, REJNALDI, SARA, SHARIK, ZOG
1135 FOR X=0 TO 7 :READ C$(X) :NEXT
1140 DATA "","RED ","GREEN ","BLUE ","TIN ","WHITE ","PALE ","GOLD "
1145 FOR X=0 TO 7 :READ D$(X) :NEXT
1150 DATA ""," OF WAR"," OF AGILITY"," OF PAIN"," OF CUNNING"
1155 DATA " OF SKILL",""," OF MAGIC"
1160 FOR X=0 TO 7 :READ W$(X) :NEXT
1165 DATA DAGGER, STAFF, HAMMER, MACHETE, AXE, SWORD, LABRYS, HALBERD
1170 FOR X=0 TO 7 :READ A$(X) :NEXT
1175 DATA LEATHER, HAUBERK, RINGMAIL, MESH, MAIL, SPLINT, PLATE, MITHRIL
1180 REM FOREST ELEMENTS
1185 FO(0) = $41 :REM TREE
1190 FO(1) = $2E :REM DOT
1195 FO(2) = $2E :REM DOT
1200 FO(3) = $43 :REM HORIZ LINE
1205 FO(4) = $42 :REM VERT LINE
1210 FO(5) = $23 :REM ARMOR (#)
1215 FO(6) = $24 :REM FOOD ($)
1220 FO(7) = $27 :REM WEAPON (')
1225 FO(8) = $27
1230 FO(9) = $24
1235 FO(10) = $2A :REM AMULET OF YENDOR
1240 RETURN
1245 REM =================================================================
1250 REM
1255 REM
1260 REM       MOVE MONSTER:NN ADDRESS:AA RELATIVE_POS:XX,YY
1265 REM
1270 REM
1275 REM =================================================================
1280 VPOKE 1,AA,SO(NN)  :REM REDRAW TILE
1285 DX=0 :DY=0
1290 IF RND(1)>0.5 GOTO 1305
1295 IF XX<0 THEN AA=AA+2 :DX=1 :GOTO 1315
1300 IF XX>0 THEN AA=AA-2 :DX=-1 :GOTO 1315
1305 IF YY<0 THEN AA=AA+$100 :DY=1 :GOTO 1315
1310 IF YY>0 THEN AA=AA-$100 :DY=-1 :GOTO 1315
1315 V=VPEEK(1,AA)
1320 IF V < $20 OR V = $2A GOTO 1350
1325 SO(NN)=VPEEK(1,AA)
1330 MA(NN)=AA
1335 XM(NN)=XM(NN)+DX
1340 YM(NN)=YM(NN)+DY
1345 VPOKE 1,AA,NN
1350 RETURN
1355 REM =================================================================
1360 REM
1365 REM
1370 REM                  MONSTER NN ATTACKS
1375 REM
1380 REM
1385 REM =================================================================
1390 LOCATE 51, 6
1395 ? LI$ : ? "\X91";
1400 W = INT(RND(1)*4)
1405 IF INT(RND(1)*40) < R7 THEN W=0
1410 IF W=0 THEN ? SPC(6) "THE " M$(NN) " MISSES."
1415 IF W>0 THEN ? SPC(6) "THE " M$(NN) " HITS YOU FOR" W "POINTS." :GOSUB 2180
1420 C1 = C1 - W
1425 RETURN
1430 REM =================================================================
1435 REM
1440 REM
1445 REM                 ATTACK MONSTER NN
1450 REM
1455 REM
1460 REM =================================================================
1465 LOCATE 52, 6
1470 ? LI$ : ? "\X91";
1475 T1 = R6 AND 7
1480 T2 = (R6/8) AND 7
1485 T3 = (R6/64) AND 7
1490 T4 = (R6/256) AND 7
1495 T5 = T1+T2+T3+T4+1 - R4
1500 IF T5 < 2 THEN T5 = 2
1505 W = INT(RND(1)*T5)
1510 HU=HU+1
1515 N=RND(1) * NN
1520 ? SPC(6) "YOU HIT THE " M$(NN) " FOR" W "POINTS"; :GOSUB 2180
1525 IF W > N THEN ? ", KILLING IT";: VPOKE 1,MA(NN),$2E :YM(NN)=-1000 :R3=R3+W
1530 ? "."
1535 RETURN
1540 REM -----------------------------------------
1545 REM
1550 REM  PANEL UTILITY 
1555 REM
1560 REM -----------------------------------------
1565 REM
1570 REM  NIFTY DISPLAY PANELS
1575 REM
1580 REM 
1585 REM ÕÃÃÃÃÃÃÃÉ
1590 REM Â       Â
1595 REM Â       Â
1600 REM ÊÃÃÃÃÃÃÃË
1605 REM 
1610 REM  ------------------------------------
1615 REM  SET UP PANEL ARRAYS BASED ON MAX
1620 REM  NUMBER OF PANELS (N=6)
1625 REM  ------------------------------------
1630 N = 6
1635 P7 = 0
1640 DIM P0$(N)
1645 DIM P1(N), P2(N), P3(N), P4(N)
1650 DIM P5$(N,30), P6(N)
1655 REM  -----------------------------------
1660 REM  HORIZONTAL BAR ARRAY
1665 REM  -----------------------------------
1670 DIM HB$(80)
1675 HB$(0) = "\XC3"
1680 FOR V=1 TO 80
1685    HB$(V) = HB$(V-1) + "\XC3"
1690 NEXT
1695 REM  -----------------------------------
1700 REM  VERTICAL BAR ARRAY
1705 REM  -----------------------------------
1710 DIM VB$(30)
1715 VB$(0) = "\XC2\X11\X9D"
1720 FOR V=1 TO 30
1725    VB$(V) = VB$(V-1) + "\XC2\X11\X9D"
1730 NEXT
1735 REM  ----------------------------------
1740 REM  PANEL EDGES
1745 REM  ----------------------------------
1750 BA$ = "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1755 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1760 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1765 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1770 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1775 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1780 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1785 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1790 SP$ = "                    "
1795 SP$ = SP$ + "                    "
1800 SP$ = SP$ + "                    "
1805 SP$ = SP$ + "                    "
1810 RETURN
1815 REM ------------------------------------
1820 REM
1825 REM DRAW PANELS FROM VARIABLE DATA
1830 REM
1835 REM ------------------------------------
1840 FOR N = 0 TO P7 - 1
1845    LOCATE P1(N), P2(N)
1850    ? "\XD5\XC3" P0$(N) "\XC3";
1855    ? HB$(P3(N) - 3 - LEN(P0$(N)));
1860    ? "\XC9\X11";
1865    B2$ = LEFT$(BA$, P3(N)+2)
1870    S2$ = LEFT$(SP$, P3(N))
1875    FOR RR=3 TO P4(N)
1880    REM DON'T FORGET THE LINES !!
1885    ? B2$ "\XC2" S2$ "\XC2\X11";
1890    NEXT
1895    ? B2$ "\XCA";
1900    ? HB$(P3(N)-1);
1905    ? "\XCB";
1910 NEXT
1915 RETURN
1920 REM ------------------------------------
1925 REM
1930 REM SHOW THE PANEL TEXTS
1935 REM
1940 REM ------------------------------------
1945 FOR N = 0 TO P7 - 1
1950    LOCATE P1(N)
1955    ?
1960    FOR X = 0 TO P4(N)-3
1965       ? SPC(P2(N)+2) P5$(N,X)
1970    NEXT
1975 NEXT
1980 RETURN
1985 REM ------------------------------------
1990 REM
1995 REM ADD A PANEL TO THE LIST!
2000 REM T: TOP, L: LEFT, W: WIDTH, H:HEIGHT
2005 REM L$: LABEL
2010 REM
2015 REM ------------------------------------
2020 N = P7
2025 P0$(N) = L$
2030 P1(N) = T
2035 P2(N) = L
2040 P3(N) = W
2045 P4(N) = H
2050 P6(N) = -1
2055 P7 = N + 1
2060 RETURN
2065 REM ------------------------------------------------------
2070 REM
2075 REM "DELETE" ALL PANELS
2080 REM
2085 REM ------------------------------------------------------
2090 P7 = 0
2095 RETURN
2100 REM ------------------------------------------------------
2105 REM
2110 REM SET CONTENTS OF A PANEL
2115 REM N:PANEL NUM, BU$(): LINES, LC: LINE COUNT
2120 REM
2125 REM ------------------------------------------------------
2130 FOR X = 0 TO LC-1
2135    P5$(N,X) = BU$(X)
2140 NEXT
2145 RETURN
2150 IF PEEK($400)=0 THEN LOAD "RF-EFFECTS.PRG",8,1,$0400
2155 RETURN
2160 SYS $0400
2165 T = TI 
2170 IF TI < T + 100 GOTO 2170
2175 RETURN
2180 SYS $0403
2185 T = TI 
2190 IF TI < T + 100 GOTO 2190
2195 RETURN
2200 RETURN
2205 SYS $0409
2210 T = TI
2215 IF TI < T + 300 GOTO 2215
2220 RETURN
2225 REM =================================================================
2230 REM
2235 REM
2240 REM                       "SPLASH" SCREEN
2245 REM
2250 REM
2255 REM =================================================================
2260 L= 1  :T= 5  :W= 76  :H= 13  :L$= "WELCOME"     :GOSUB 2020
2265 L= 1  :T= 19 :W= 76  :H= 15  :L$= "MAP KEY"     :GOSUB 2020
2270 L= 1  :T= 35 :W= 76  :H= 12  :L$= "COMMAND KEY" :GOSUB 2020
2275 GOSUB 1840 :REM DRAW PANELS
2280 LOCATE 8
2285 ? SPC(6) "FIND THE AMULET OF YENDOR AND EXIT THE FOREST ALIVE!"
2290 ?
2295 ? SPC(6) "POINTS ARE AWARDED FOR ITEMS COLLECTED AND MONSTERS KILLED."
2300 LOCATE 21
2305 ? SPC(6) "@ ... YOU"
2310 ? SPC(6) CHR$(FO(0)) " ... TREE (IMPASSABLE BY YOU)"
2315 ? SPC(6) CHR$(FO(1)) " ... CLEARING"
2320 ? SPC(6) CHR$(FO(5)) " ... ARMOR"
2325 ? SPC(6) CHR$(FO(6)) " ... FOOD"
2330 ? SPC(6) CHR$(FO(7)) " ... WEAPON"
2335 ? SPC(6) CHR$(FO(10))" ... AMULET OF YENDOR"
2340 ?:?
2345 ? SPC(6) "-- ANYTHING THAT MOVES IS A MONSTER --"
2350 LOCATE 37
2355 ? SPC(6) "? ............ HELP"
2360 ? SPC(6) "CURSOR KEYS .. MOVE / ATTACK"
2365 ? SPC(6) ". ............ REST"
2370 ? SPC(6) "E ............ EAT FOOD"
2375 ? SPC(6) "X ............ EXIT"
2380 ?
2385 LOCATE 50
2390 ? "   GOOD LUCK!"
2395 ?
2400 GOSUB 2205
2405 INPUT "   PLEASE TYPE YOUR NAME"; R0$
2410 X=RND(-TI)
2415 RETURN
2420 REM =================================================================
2425 REM
2430 REM
2435 REM                   TREASURE FINDS (TYPE IN 'LO')
2440 REM
2445 REM
2450 REM =================================================================
2455 COLOR 1,6
2460 REM CLEAR THE SQUARE THE PLAYER IS *LOOKING AT*
2465 VPOKE 1,(R2+YY)*$100 + (R1+XX)*2, $2E
2470 GOSUB 2160
2475 REM  ----------------------
2480 REM  PRE-CALC SOME VALUES
2485 REM  ----------------------
2490 T1=INT(RND(1)*8)
2495 T2=INT(RND(1)*8)
2500 T3=INT(RND(1)*8)
2505 T4=INT(RND(1)*8)
2510 REM  ------------------------
2515 REM  FIGURE OUT WHAT WE GOT
2520 REM  ------------------------
2525 IF LO=FO(10) GOTO 2550
2530 IF LO=FO(5)  GOTO 2610
2535 IF LO=FO(6)  GOTO 2580
2540 IF LO=FO(7)  GOTO 2665
2545 RETURN
2550 LOCATE 5
2555 ? LI$ :? "\X91   YOU FOUND THE AMULET OF YENDOR! "
2560 R8$ = "FOUND IT!"
2565 R8=1  :REM PLAYER-YENDOR
2570 T0 = 5
2575 RETURN
2580 T1 = INT(RND(1)*8+1)
2585 LOCATE 5
2590 ? LI$ :?"\X91   YOU FOUND" T1 "FOOD."
2595 R5=R5 + T1
2600 T0 = 5
2605 RETURN
2610 R3 = R3 + T1+T2+T3+T4
2615 AD$ = N$(T1) + "'S " + C$(T2) + A$(T3) + D$(T4)
2620 GOSUB 2720
2625 ? SPC(6) "YOU FOUND " AD$
2630 ?:? SPC(6) "REPLACE YOUR CURRENT ARMOR WITH IT? [YN] "; 
2635 GET YN$ :IF YN$<> "Y" AND YN$ <> "N" GOTO 2635
2640 GOSUB 2750
2645 IF YN$ = "N" THEN RETURN
2650 R7 = T1 + T2 +T3 + T4 
2655 R7$ = AD$
2660 RETURN
2665 R3 = R3 + T1+T2+T3+T4
2670 WD$ = N$(T1) + "'S " + C$(T2) + W$(T3) + D$(T4)
2675 GOSUB 2720
2680 ? SPC(6) "YOU FOUND " WD$
2685 ?:? SPC(6) "REPLACE YOUR CURRENT WEAPON WITH IT? [YN] "; 
2690 GET YN$ :IF YN$<> "Y" AND YN$ <> "N" GOTO 2690
2695 GOSUB 2750
2700 IF YN$ = "N" THEN RETURN
2705 R6 = T1 + T2 * 8 + T3 * 64 + T4 * 256
2710 R6$ = WD$
2715 RETURN
2720 LOCATE 51
2725 ?SPC(6) LI$
2730 ?SPC(6) LI$
2735 ?SPC(6) LI$
2740 ?"\X91\X91\X91";
2745 RETURN
2750 ? "\X91\X91\X91\X91"
2755 ?SPC(6) LI$ 
2760 ?SPC(6) LI$ 
2765 ?SPC(6) LI$ 
2770 ?SPC(6) LI$
2775 RETURN
