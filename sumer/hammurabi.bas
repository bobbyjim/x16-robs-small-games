5 	SC = 32 :REM SCREEN INDENT
10 	? CHR$(147) "\X11" 
15 	? TAB(SC) "HAMMURABI"
20 	? TAB(SC/2) "\X11\X11CREATIVE COMPUTING   MORRISTOWN, NEW JERSEY   1978"
25 	? "\X11\X11TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA"
30 	? "FOR A TWELVE-YEAR TERM OF OFFICE." 
35 	INPUT "\X11\X11PRESS <ENTER> TO BEGIN"; E$
40 	X=RND(-TI) :REM SRAND
45 	D1=0   :REM TOTAL DEATHS
50 	P1=0   :REM STARVATIONS PER YEAR
55 	Z=0    :REM CURRENT YEAR
60 	P=95   :REM POPULATION
65 	S=2800 :REM STORES OF GRAIN
70 	H=3000
75 	E=H-S  :REM RAT DEPREDATIONS
80 	Y=3    :REM YIELD PER ACRE
85 	A=H/Y  :REM ACRES OF FARMLAND
90 	I=5    :REM IMMIGRATED
95 	Q=1    :REM PLAGUE (FOR SUMMARY USE)
100 	D=0    :REM DIED
105 	Z=Z+1
110 	? "\X11\X11\X11HAMMURABI: I BEG TO REPORT TO YOU, IN YEAR" Z ",\X11"
115 	? TAB(SC/2) D "PEOPLE STARVED."
120 	? TAB(SC/2) I "PEOPLE CAME TO THE CITY.\X11"
125 	P=P+I
130 	IF Q>0 GOTO 145
135 	P=INT(P/2)
140 	? TAB(SC/2) "A HORRIBLE PLAGUE STRUCK! HALF THE POPULATION DIED!\X11"
145 	? TAB(SC/2) "CURRENT POPULATION        :" P
150 	? TAB(SC/2) "ACRES OWNED BY THE CITY   :" A
155 	? TAB(SC/2) "BUSHELS HARVESTED PER ACRE:" Y
160 	? TAB(SC/2) "BUSHELS EATEN BY RATS     :" E
165 	? TAB(SC/2) "BUSHELS IN STORE          :" S
170 	IF Z=13 GOTO 545
175 	C = INT(10*RND(1)): Y=C+17
180 	? "\X11LAND IS TRADING AT" Y "BUSHELS PER ACRE.\X11"
185 	INPUT "HOW MANY ACRES DO YOU WISH TO BUY"; Q$
190 	Q=VAL(Q$)
195 	IF Q<0 GOTO 510
200 	IF Q=0 GOTO 235
205 	IF Y*Q <= S GOTO 220
210 	GOSUB 525
215 	GOTO 185
220 	A=A+Q :S=S-Y*Q :C=0
225 	?
230 	GOTO 280
235 	INPUT "HOW MANY ACRES DO YOU WISH TO SELL"; Q$
240 	Q=VAL(Q$)
245 	IF Q<0 GOTO 510
250 	IF Q=0 THEN ? :GOTO 280
255 	IF Q<A GOTO 270
260 	GOSUB 535
265 	GOTO 235
270 	A=A-Q :S=S+Y*Q :C=0
275 	?
280 	INPUT "HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE"; Q$
285 	Q=VAL(Q$)
290 	IF Q<0 GOTO 510
295 	IF Q=0 GOTO 325
300 	IF Q <= S GOTO 315
305 	GOSUB 525
310 	GOTO 280	
315 	S=S-Q :C=1
320 	?
325 	INPUT "HOW MANY ACRES DO YOU WISH TO PLANT WITH SEED"; D$
330 	D=VAL(D$)
335 	IF D=0 GOTO 395
340 	IF D<0 GOTO 510
345 	IF D<=A GOTO 360
350 	GOSUB 535
355 	GOTO 325
360 	IF INT(D/2) <= S GOTO 375
365 	GOSUB 525
370 	GOTO 325
375 	IF D<=10*P GOTO 390
380 	? "BUT YOU HAVE ONLY" P "PEOPLE TO TEND THE FIELDS!  NOW THEN,"
385   	GOTO 325
390 	S=S-INT(D/2)
395 	GOSUB 500
400 	REM A BOUNTIFUL HARVEST!
405 	Y=C :H=D*Y :E=0
410 	GOSUB 500
415 	IF INT(C/2)<>C/2 GOTO 430
420 	REM RATS RUN WILD!
425 	E=INT(S/C) 
430 	S=S-E+H	
435 	GOSUB 500
440 	REM POPULATION INCREASE
445 	I = INT(C*(20*A+S)/P/100+1)
450 	REM CHECK HUNGER LEVELS
455 	C=INT(Q/20)
460 	REM 15% CHANCE OF PLAGUE
465 	Q=INT(10*(2*RND(1)-.3))
470 	IF P<C THEN 100	
475 	REM STARVE ENOUGH FOR IMPEACHMENT
480 	D=P-C :IF D>.45*P GOTO 640
485 	P1=((X-1)*P1+D*100/P)/Z
490 	P=C :D1=D1+D 
495 	GOTO 105
500 	C = INT(RND(1)*5)+1
505 	RETURN
510 	? "HAMMURABI: I CANNOT DO WHAT YOU WISH."
515 	? "GET YOURSELF ANOTHER STEWARD!!!!!"
520 	GOTO 710
525 	? "HAMMURABI: THINK AGAIN. YOU HAVE ONLY" S "BUSHELS OF GRAIN. NOW THEN,"
530 	RETURN
535 	? "HAMMURABI: THINK AGAIN. YOU OWN ONLY" A "ACRES. NOW THEN,"
540 	RETURN
545         IF D1 > 0 THEN 560
550         ? "\X11IN YOUR TERM OF OFFICE, NO-ONE DIED FROM STARVATION."
555         GOTO 575
560 	? "\X11IN YOUR TERM OF OFFICE," P1 "PERCENT OF THE"
565 	? "POPULATION STARVED PER YEAR ON THE AVERAGE, I.E. A TOTAL OF"
570 	? D1 "PEOPLE DIED!!" 
575 	L% = A/P
580 	? "\X11YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
585 	? L% "ACRES PER PERSON."
590 	?
595 	IF P1>33 GOTO 650
600 	IF L<7   GOTO 650
605 	IF P1>10 GOTO 670
610 	IF L<9   GOTO 670
615 	IF P1>3  GOTO 690
620 	IF L<10  GOTO 690
625 	? "A FANTASTIC PERFORMANCE!!! CHARLEMAGNE, DISRAELI, AND"
630 	? "JEFFERSON COMBINED COULD NOT HAVE DONE BETTER!"
635 	GOTO 710
640 	?
645 	? "YOU STARVED" D "PEOPLE IN ONE YEAR!!!"
650 	? "DUE TO THIS EXTREME MISMANAGEMENT YOU HAVE NOT ONLY"
655 	? "BEEN IMPEACHED AND THROWN OUT OF OFFICE BUT YOU HAVE"
660 	? "ALSO BEEN DECLARED NATIONAL FINK!!!!"
665 	GOTO 710
670 	? "YOUR HEAVY-HANDED PERFORMANCE SMACKS OF NERO AND IVAN IV."
675 	? "THE PEOPLE (REMAINING) FIND YOU AN UNPLEASANT RULER, AND,"
680 	? "FRANKLY, HATE YOUR GUTS!!" 
685 	GOTO 710
690 	? "YOUR PERFORMANCE COULD HAVE BEEN SOMEWHAT BETTER, BUT"
695 	? "REALLY WASN'T TOO BAD AT ALL. " INT(P*.8*RND(1)) "PEOPLE"
700 	? "DEARLY LIKE TO SEE YOU ASSASSINATED BUT WE ALL HAVE OUR"
705 	? "TRIVIAL PROBLEMS."
710 	? "SO LONG FOR NOW."
715 	?
720 	END
