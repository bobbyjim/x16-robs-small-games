

	sc = 32 :rem screen indent
	? chr$(147) "\x11" 
	? tab(sc) "hammurabi"
	? tab(sc/2) "\x11\x11creative computing   morristown, new jersey   1978"
	? "\x11\x11try your hand at governing ancient sumeria"
	? "for a twelve-year term of office." 
	input "\x11\x11press <enter> to begin"; e$

	x=rnd(-ti) :rem srand
	d1=0   :rem total deaths
	p1=0   :rem starvations per year
	z=0    :rem current year
	p=95   :rem population
	s=2800 :rem stores of grain
	h=3000
	e=h-s  :rem rat depredations
	y=3    :rem yield per acre
	a=h/y  :rem acres of farmland
	i=5    :rem immigrated
	q=1    :rem plague (for summary use)

{:summary-without-plague}
	d=0    :rem died

{:summary}
	z=z+1
	? "\x11\x11\x11hammurabi: i beg to report to you, in year" z ",\x11"
	? tab(sc/2) d "people starved."
	? tab(sc/2) i "people came to the city.\x11"
	p=p+i
	if q>0 goto {:skip-plague}
	p=int(p/2)
	? tab(sc/2) "a horrible plague struck! half the population died!\x11"

{:skip-plague}
	? tab(sc/2) "current population        :" p
	? tab(sc/2) "acres owned by the city   :" a
	? tab(sc/2) "bushels harvested per acre:" y
	? tab(sc/2) "bushels eaten by rats     :" e
	? tab(sc/2) "bushels in store          :" s

	if z=13 goto {:game-over}
	c = int(10*rnd(1)): y=c+17
	? "\x11land is trading at" y "bushels per acre.\x11"

{:buy}
	input "how many acres do you wish to buy"; q$
	q=val(q$)
	if q<0 goto {:no-can-do}
	if q=0 goto {:sell}
	if y*q <= s goto {:buy-some-land}
	gosub {:not-enough-bushels}
	goto {:buy}

{:buy-some-land}
	a=a+q :s=s-y*q :c=0
	?
	goto {:eat}

{:sell}
	input "how many acres do you wish to sell"; q$
	q=val(q$)
	if q<0 goto {:no-can-do}
	if q=0 then ? :goto {:eat}
	if q<a goto {:sell-some-land}
	gosub {:not-enough-acres}
	goto {:sell}

{:sell-some-land}
	a=a-q :s=s+y*q :c=0
	?
{:eat}
	input "how many bushels do you wish to feed your people"; q$
	q=val(q$)
	if q<0 goto {:no-can-do}
	if q=0 goto {:planting}
	if q <= s goto {:feed-the-people}
	gosub {:not-enough-bushels}
	goto {:eat}	

{:feed-the-people}
	s=s-q :c=1
	?

{:planting}
	input "how many acres do you wish to plant with seed"; d$
	d=val(d$)
	if d=0 goto {:harvest-time}
	if d<0 goto {:no-can-do}
	if d<=a goto {:check-bushels}
	gosub {:not-enough-acres}
	goto {:planting}

{:check-bushels}
	if int(d/2) <= s goto {:check-labor-force}
	gosub {:not-enough-bushels}
	goto {:planting}

{:check-labor-force}
	if d<=10*p goto {:plant-the-fields}
	? "but you have only" p "people to tend the fields!  now then,"
  	goto {:planting}

{:plant-the-fields}
	s=s-int(d/2)

{:harvest-time}
	gosub {:set-random-c}
	rem a bountiful harvest!
	y=c :h=d*y :e=0

	gosub {:set-random-c}
	if int(c/2)<>c/2 goto {:fill-granary}
	rem rats run wild!
	e=int(s/c) 

{:fill-granary}
	s=s-e+h	

	gosub {:set-random-c}
	rem population increase
	i = int(c*(20*a+s)/p/100+1)

	rem check hunger levels
	c=int(q/20)

	rem 15% chance of plague
	q=int(10*(2*rnd(1)-.3))
	if p<c then {:summary-without-plague}	

	rem starve enough for impeachment
	d=p-c :if d>.45*p goto {:impeachment-due-to-starvation}
	p1=((x-1)*p1+d*100/p)/z
	p=c :d1=d1+d 
	goto {:summary}


{:set-random-c}
	c = int(rnd(1)*5)+1
	return

{:no-can-do}
	? "hammurabi: i cannot do what you wish."
	? "get yourself another steward!!!!!"
	goto {:so-long-for-now}

{:not-enough-bushels}
	? "hammurabi: think again. you have only" s "bushels of grain. now then,"
	return

{:not-enough-acres}
	? "hammurabi: think again. you own only" a "acres. now then,"
	return

{:game-over}
        if d1 > 0 then {:starvation-report}
        ? "\x11in your term of office, no-one died from starvation."
        goto {:land-report}

{:starvation-report}
	? "\x11in your term of office," p1 "percent of the"
	? "population starved per year on the average, i.e. a total of"
	? d1 "people died!!" 

{:land-report}
	l% = a/p
	? "\x11you started with 10 acres per person and ended with"
	? l% "acres per person."
	?
	if p1>33 goto {:extreme-mismanagement}
	if l<7   goto {:extreme-mismanagement}
	if p1>10 goto {:heavy-handed}
	if l<9   goto {:heavy-handed}
	if p1>3  goto {:not-bad}
	if l<10  goto {:not-bad}

{:fantastic}
	? "a fantastic performance!!! charlemagne, disraeli, and"
	? "jefferson combined could not have done better!"
	goto {:so-long-for-now}

{:impeachment-due-to-starvation}
	?
	? "you starved" d "people in one year!!!"

{:extreme-mismanagement}
	? "due to this extreme mismanagement you have not only"
	? "been impeached and thrown out of office but you have"
	? "also been declared national fink!!!!"
	goto {:so-long-for-now}

{:heavy-handed}
	? "your heavy-handed performance smacks of nero and ivan iv."
	? "the people (remaining) find you an unpleasant ruler, and,"
	? "frankly, hate your guts!!" 
	goto {:so-long-for-now}

{:not-bad}
	? "your performance could have been somewhat better, but"
	? "really wasn't too bad at all. " int(p*.8*rnd(1)) "people"
	? "dearly like to see you assassinated but we all have our"
	? "trivial problems."

{:so-long-for-now}
	? "so long for now."
	?
	end

