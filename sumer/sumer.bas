5 REM -----------------------------------------------------
10 REM
15 REM   SUMER - A HMRABI / SUMER GAME FROM SCRATCH
20 REM   RJE 2021 (ADAPTED FROM MY 1983-ISH VERSION)
25 REM
30 REM -----------------------------------------------------
35 DIM DN$(59),CL$(59)
40 CL$(0)=CHR$(147) :REM CLS
45 DN$(0)=CHR$(19)  :REM HOME
50 FOR X = 1 TO 59
55    DN$(X)=DN$(X-1)+"\X11"
60    CL$(X)=CL$(X-1)+"\X11"
65 NEXT
70 SP$ = "     " : S2$ = "               "
75 HR$=CHR$(192)
80 FOR X = 1 TO 79:HR$=HR$+CHR$(192) :NEXT
85 EL$="                                                                     "
90 AS$="ACCURSED SEA PEOPLES"
95 REM EMULATE AMBER PETLIKE TERMINAL
100 COLOR 8,0  
105 VLOAD "PETFONT.BIN",8,1,0 
110 POKE $9F36,128
115 REM --------------------------------------------------
120 REM  TITLE SCREEN
125 REM --------------------------------------------------
130 ? CL$(16) SPC(30) "        SUMER" 
135 ? HR$ SPC(30) "  RJE 1983 (UPDATED)"
140 ? "\X11\X11\X11\X11\X11"
145 ? SP$ "RUN THE ANCIENT CITY-STATE OF SUMER FOR TWELVE YEARS.  USE YOUR\X11"
150 ? SP$ "WISDOM TO GROW A STRONG POPULATION.  PREPARE AGAINST DISASTER, AND\X11"
155 ? SP$ "BE RECORDED AS A WORTHY RULER.\X11\X11\X11\X11\X11"
160 INPUT "     DO YOU WANT INSTRUCTIONS"; YN$
165 IF YN$<>"Y" AND YN$<>"N" GOTO 130
170 IF YN$="Y" THEN GOSUB 485
175 REM --------------------------------------------------
180 REM  INITIALIZE DATA
185 REM --------------------------------------------------
190 X=RND(-TI)
195 A1 = 0 :REM PLAGUE
200 A2 = 0 :REM FAMINE
205 A3 = 0 :REM AT WAR
210 A4 = 0 :REM ATTACK/ED
215 A8 = 100 :REM POP
220 A9 = 1.0 :REM HEALTH
225 B0 = 1.0 :REM LOYALTY
230 B1 = 0 :REM BIRTHS
235 B4 = 0 :REM IMMIGRANTS
240 A5 = 1000 :REM ACRES.TOTAL
245 A6 = 1000 :REM ACRES.PLANTED
250 A7 = 20 :REM ACRES.PRICE
255 B7 = 3000 :REM GRAIN.STORE
260 B6 = 3 :REM GRAIN.HARVEST
265 B8 = 0 :REM GRAIN.EATEN
270 B9 = 0 :REM GRAIN.SPOILT
275 REM --------------------------------------------------
280 REM  MAIN LOOP
285 REM --------------------------------------------------
290 FOR A0 = 3510 TO 3501 STEP -1
295 B0 = B0 * 0.95 :REM LOYALTY SLOWLY DIMINISHES
300 GOSUB 615     :REM START REPORT
305 GOSUB 660 :REM CHECK POPULATION
310 GOSUB 715     :REM CHECK PLAGUE
315 GOSUB 750     :REM CHECK FAMINE
320 GOSUB 780        :REM CHECK WAR
325 GOSUB 815  :REM COMPLETE REPORT
330 ? DN$(49) HR$;
335 GOSUB 890 :REM BUY/SELL LAND
340 GOSUB 990 :REM FEED PEOPLE
345 GOSUB 1060 :REM PLANT FIELDS
350 IF A0<3512 THEN GOSUB 1135 :REM WAGE WAR
355 ?: INPUT "   PRESS <RETURN>"; YN$
360 GOSUB 1180 :REM HANDLE ALL EVENTS
365 NEXT A0 :REM NEXT YEAR
370 REM --------------------------------------------------
375 REM  END GAME
380 REM --------------------------------------------------
385 ? CL$(10) SPC(30) "      END OF TERM"
390 ? HR$ "\X11"
395 P1 = INT(B3 * 100)
400 L  = INT(0.5 + A5 / A8)
405 ? SP$ "O GREAT HAMMURABI, THE YEAR IS"A0"BC\X11"
410 ? SP$ "IN YOUR 12-YEAR TERM OF OFFICE," P1 "% OF THE"
415 ? SP$ "PEOPLE STARVED EACH YEAR ON AVERAGE; THAT IS," B2 "PEOPLE STARVED!"
420 ?
425 ? SP$ "YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
430 ? SP$ L "ACRES PER PERSON."
435 IF P1>33 OR L<7  GOTO 455
440 IF P1>10 OR L<9  GOTO 460
445 IF P1>3  OR L<10 GOTO 465
450 ? SP$ "YOU WERE RECORDED AS A WISE AND JUST RULER!" :END
455 ? SP$ "YOU WERE RECORDED AS THE INCOMPETENT NATIONAL FINK!" :END
460 ? SP$ "YOU WERE RECORDED AS HEAVY-HANDED AND UNPOPULAR." :END 
465 ? SP$ "YOU WERE RECORDED AS A SO-SO RULER." :END
470 REM --------------------------------------------------
475 REM  INSTRUCTIONS
480 REM --------------------------------------------------
485 ? CL$(10)
490 READ A$
495 ? SP$ A$ "\X11"
500 IF A$ <> "" GOTO 490
505 DATA "THE YEAR IS 3500 BC.  YOU ARE THE RULER OF THE ANCIENT STATE OF"
510 DATA "SUMER. YOU HAVE TEN YEARS TO RUN YOUR EMPIRE."
515 DATA "\X11"
520 DATA "FEED EACH PERSON 20 BUSHELS OF GRAIN PER YEAR; LESS WILL DECREASE"
525 DATA "HEALTH AND LOYALTY, WHILE MORE WILL INCREASE BOTH. A HEALTHY "
530 DATA "POPULATION IS STRONGER AND GROWS FASTER."
535 DATA "\X11"
540 DATA "EACH PERSON CAN PLANT TWO ACRES OF FARMLAND WITH ONE BUSHEL OF GRAIN."
545 DATA "EACH ACRE YIELDS BETWEEN 2 AND 7 BUSHELS OF GRAIN. FAMINE REDUCES"
550 DATA "THE YIELD PER ACRE, AND RATS EAT SURPLUS STORED GRAIN."
555 DATA "\X11"
560 DATA "THE PRICE OF LAND AVERAGES 20 BUSHELS PER ACRE."
565 DATA "\X11"
570 DATA "WAR AND PLAGUE WILL KILL PEOPLE AND LOWER LOYALTY.
575 DATA "WINNING A BATTLE WILL INCREASE LOYALTY.  DO NOT START A FIGHT IF THE "
580 DATA "POPULATION IS IN POOR HEALTH."
585 DATA ""
590 ?:INPUT "     PRESS <RETURN> TO BEGIN"; YN$
595 RETURN
600 REM --------------------------------------------------
605 REM  START THE REPORT
610 REM --------------------------------------------------
615 ? CL$(4);
620 ? SP$ "O KING OF SHURUPPAK, I AM SORRY TO REPORT, ";
625 ? "IN YEAR" (3513 - A0);
630 ? "(" A0 "BC )"
635 ? HR$ "\X11"
640 RETURN
645 REM --------------------------------------------------
650 REM CHECK POPULATION
655 REM --------------------------------------------------
660 IF B1 > 0 THEN ? S2$ "** " B1 "NEW CITIZENS WERE BORN\X11"
665 IF B1 < 0 THEN ? S2$ "** " ABS(B1) "PEOPLE STARVED TO DEATH!\X11"
670 IF B1 < 0 THEN B2 = B2 - B1
675 X = 3513 - A0
680 IF B1 < 0 THEN B3 = B3 + (B1 / A8)/12
685 IF B4 > 0 THEN ? S2$ "** " B4 "PEOPLE MOVED TO SUMER\X11"
690 IF B4 < 0 THEN ? S2$ "** " B4 "PEOPLE LEFT SUMER\X11"
695 RETURN
700 REM --------------------------------------------------
705 REM CHECK PLAGUE
710 REM --------------------------------------------------
715 IF A1 = 0 THEN ? S2$ "THE STREETS ARE CLEAN\X11" :RETURN
720 ? S2$ "A PLAGUE STRUCK YOUR CITY!\X11"
725 ? S2$ "** " A1 "PEOPLE DIED\X11"
730 RETURN :REM PLAGUE DONE
735 REM --------------------------------------------------
740 REM CHECK FAMINE
745 REM --------------------------------------------------
750 IF A2 = 0 THEN ? S2$ "THE LAND IS NOW HEALTHY\X11" :RETURN
755 ? S2$ "THERE IS FAMINE IN THE LAND!\X11" :RETURN 
760 RETURN :REM FAMINE DONE
765 REM --------------------------------------------------
770 REM CHECK WAR
775 REM --------------------------------------------------
780 IF A4 > 0 THEN ? S2$ "SUMER ATTACKS THE "AS$"!\X11":RETURN
785 IF A4 < 0 THEN ? S2$ "SUMER WAS ATTACKED!\X11" :RETURN
790 IF A3>0 THEN?S2$"YOU ARE AT WAR WITH THE "AS$"!\X11" :RETURN
795 ? S2$ "SUMER IS AT PEACE.\X11" :RETURN
800 REM --------------------------------------------------
805 REM  COMPLETE THE REPORT
810 REM --------------------------------------------------
815 ? 
820 ? S2$ "SUMER HAS" A8 "PEOPLE;\X11"
825 ? S2$ "IT HAS"A5"ACRES OF FARMLAND;"A6"WERE PLANTED.\X11"
830 REM RATS?
835 IF B8>0 THEN ? S2$ "RATS ATE"B8"BUSHELS OF STORED GRAIN.\X11"
840 REM SPOILAGE?
845 IF B9>0 THEN ? S2$ "\X9D" B9"BUSHELS OF STORED GRAIN SPOILED.\X11"
850 ? S2$ "THE HARVEST WAS" B6 "BUSHELS PER ACRE,\X11"
855 T = B6 * A6
860 ? SPC(25) "FOR A TOTAL OF" T "BUSHELS.\X11"
865 ? S2$ "\X11THE ROYAL GRANARIES NOW HOLD" B7 "BUSHELS OF GRAIN.\X11"
870 RETURN :REM COMPLETE REPORT
875 REM --------------------------------------------------
880 REM  BUY AND SELL LAND
885 REM --------------------------------------------------
890 LL = INT(RND(1)*10+16)
895 IF B7 < LL GOTO 935
900 ? DN$(48);EL$;DN$(48);
905 ?"  LAND IS"LL"BUSHELS PER ACRE. YOU HAVE"A5"ACRES AND"B7 "BUSHELS."
910 ? DN$(50);EL$;DN$(50);
915 INPUT "  HOW MANY ACRES WILL YOU BUY";AB$
920 AB=VAL(AB$) :IF AB*LL>B7 THEN 900
925 B7 = B7 - AB*LL
930 A5 = A5 + AB
935 ? DN$(48);EL$;DN$(48);
940 ?"  LAND IS"LL"BUSHELS PER ACRE. YOU HAVE"A5"ACRES AND"B7 "BUSHELS."
945 ? DN$(50);EL$;DN$(50);
950 INPUT "  HOW MANY ACRES WILL YOU SELL";AB$
955 AB=VAL(AB$) :IF AB>A5 THEN 935
960 B7 = B7 + AB*LL
965 A5 = A5 - AB
970 RETURN :REM BUY AND SELL LAND
975 REM --------------------------------------------------
980 REM  FEED YOUR PEOPLE
985 REM --------------------------------------------------
990 ? DN$(48);EL$;DN$(48);
995 ? "  YOU HAVE"A5"ACRES AND"B7 "BUSHELS."
1000 ? DN$(50); EL$ ; DN$(50); 
1005 INPUT "  HOW MANY BUSHELS WILL YOU USE TO FEED YOUR PEOPLE"; BU$
1010 BU = VAL(BU$) :IF BU>B7 GOTO 990
1015 B7 = B7 - BU
1020 BU = (BU / A8) - 20
1025 B0 = B0 + BU/50
1030 A9 = A9 + BU/100
1035 ? DN$(59) "LOYALTY:" B0 ", HEALTH:" A9 ", BU/POP:" BU / A8;
1040 RETURN
1045 REM --------------------------------------------------
1050 REM  PLANT
1055 REM --------------------------------------------------
1060 ? DN$(48);EL$;DN$(48);
1065 ? "  YOU HAVE"A5"ACRES AND"B7 "BUSHELS."
1070 ? DN$(50);EL$;DN$(50);
1075 INPUT "  HOW MANY ACRES WILL YOU PLANT"; PA$
1080 PA = VAL(PA$)
1085 IF PA>B7 * 2 THEN ?:? "  YOU LACK THE GRAIN" :GOTO 1060
1090 IF PA>A5 THEN ?:? "  YOU LACK THE LAND" :GOTO 1060
1095 IF PA>A8 * 10 THEN ?:? "  YOU DON'T HAVE ENOUGH PEOPLE" :GOTO 1060
1100 ? DN$(52);EL$
1105 B7 = B7 - INT(PA/2)
1110 A6 = PA 
1115 RETURN :REM PLANT
1120 REM --------------------------------------------------
1125 REM WAGE WAR ON THE ACCURSED SEA PEOPLES
1130 REM --------------------------------------------------
1135 IF A3 > 0 THEN RETURN
1140 ? DN$(50); EL$ ; DN$(50);
1145 INPUT "  DO YOU WISH TO WAGE WAR AGAINST THE ACCURSED SEA PEOPLES"; YN$
1150 A4 = 0
1155 YN=VAL(YN$) :IF YN$="Y" THEN A4 = 1
1160 RETURN
1165 REM --------------------------------------------------
1170 REM HANDLE ALL EVENTS
1175 REM --------------------------------------------------
1180 REM
1185 REM GRAIN EATEN = RAT POPULATION
1190 REM RAT POPULATION WILL GROW IF YOU KEEP
1195 REM A LARGE SURPLUS OVER MULTIPLE YEARS
1200 REM
1205 B8 = B8 + INT(RND(1)*B7 /100)*10
1210 IF B8 > B7  THEN B8 = B7 
1215 B7 = B7 - B8
1220 REM
1225 REM GRAIN ROT IS NOT CUMULATIVE, I GUESS
1230 REM
1235 B9 = INT(RND(1)*B7 /30)*10
1240 IF B9 > B7 THEN B9 = B7
1245 B7 = B7 - B9
1250 REM
1255 REM GRAIN HARVEST
1260 REM
1265 B6 = INT(RND(1)*4+4) - A2 :REM 3 TO 7 BU/ACRE NORMALLY
1270 IF B6 < 2 THEN B6 = 2
1275 B7 = B7 + B6 * A6
1280 REM
1285 REM BIRTHS
1290 REM
1295 B1 = INT( (A9-0.5) * A8 * RND(1)/8 )
1300 A8 = A8 + B1
1305 REM
1310 REM IMMIGRATION
1315 REM
1320 B4 = INT( (B0-0.5) * A8 * RND(1)/5 )
1325 A8 = A8 + B4
1330 REM
1335 REM START/END PLAGUE
1340 REM
1345 A1 = 0
1350 IF RND(1)>0.7 THEN A1 = INT(A8 * RND(1) * 0.5) 
1355 IF A1>0 THEN A9 = A9 * 0.9 :REM ERODE HEALTH
1360 A8 = A8 - A1
1365 REM
1370 REM START/END FAMINE
1375 REM
1380 IF RND(1)>0.7 THEN A2 = INT(RND(1)*4+1) 
1385 IF RND(1)<0.2 THEN A2 = 0
1390 REM
1395 REM WAGE WAR
1400 REM
1405 IF A3 = 0 GOTO 1460
1410 B0 = B0 * 1.05 :REM LOYALTY INCREASES DURING WAR
1415 A8 = A8 - INT(RND(1)*A8/20)
1420 IF RND(1)>0.5 THEN A3 = 0 
1425 REM
1430 REM IF SUMER APPEARS WEAK, THEN THE SEA PEOPLES ATTACK
1435 REM
1440 IF RND(1) > 0.5 GOTO 1460
1445 IF A2 == 0 AND A1 == 0 THEN RETURN
1450 B0 = B0 * 0.95 :REM LOYALTY DECREASES DUE TO INCURSION
1455 A4 = -1
1460 RETURN
