5 REM -----------------------------------------------------
10 REM
15 REM   SUMER - A CITY-STATE-SIM GAME FROM SCRATCH.
20 REM   RJE 2021 (ADAPTED FROM MY 1983-ISH VERSION, 
25 REM   WHICH WAS IN TURN ADAPTED FROM HAMMURABI.
30 REM
35 REM -----------------------------------------------------
40 DIM DN$(59),CL$(59)
45 CL$(0)=CHR$(147) :REM CLS
50 DN$(0)=CHR$(19)  :REM HOME
55 FOR X = 1 TO 59
60    DN$(X)=DN$(X-1)+"\X11"
65    CL$(X)=CL$(X-1)+"\X11"
70 NEXT
75 SP$ = "     " : S2$ = "               "
80 HR$=CHR$(192)
85 FOR X = 1 TO 79:HR$=HR$+CHR$(192) :NEXT
90 EL$="                                                                     "
95 AS$="ACCURSED SEA PEOPLES"
100 REM EMULATE AMBER PETLIKE TERMINAL
105 COLOR 8,0  
110 POKE $030C, 4 :REM .A=4
115 SYS $FF62 :REM PET FONT
120 CLS
125 REM --------------------------------------------------
130 REM  TITLE SCREEN
135 REM --------------------------------------------------
140 ? CL$(16) SPC(30) "        SUMER" 
145 ? HR$ SPC(30) "  RJE 1983 (UPDATED)"
150 ? "\X11\X11\X11\X11\X11"
155 ? SP$ "RUN THE ANCIENT CITY-STATE OF SUMER FOR TWELVE YEARS.  USE YOUR\X11"
160 ? SP$ "WISDOM TO GROW A STRONG POPULATION.  PREPARE AGAINST DISASTER, AND\X11"
165 ? SP$ "BE RECORDED AS A WORTHY RULER.\X11\X11\X11\X11\X11"
170 INPUT "     DO YOU WANT INSTRUCTIONS"; YN$
175 IF YN$<>"Y" AND YN$<>"N" GOTO 140
180 IF YN$="Y" THEN GOSUB 495
185 REM --------------------------------------------------
190 REM  INITIALIZE DATA
195 REM --------------------------------------------------
200 X=RND(-TI)
205 A1 = 0 :REM PLAGUE
210 A2 = 0 :REM FAMINE
215 A3 = 0 :REM AT WAR
220 A4 = 0 :REM ATTACK/ED
225 A8 = 100 :REM POP
230 A9 = 1.0 :REM HEALTH
235 B0 = 1.0 :REM LOYALTY
240 B1 = 0 :REM BIRTHS
245 B4 = 0 :REM IMMIGRANTS
250 A5 = 1000 :REM ACRES.TOTAL
255 A6 = 1000 :REM ACRES.PLANTED
260 A7 = 20 :REM ACRES.PRICE
265 B7 = 3000 :REM GRAIN.STORE
270 B6 = 3 :REM GRAIN.HARVEST
275 B8 = 0 :REM GRAIN.EATEN
280 B9 = 0 :REM GRAIN.SPOILT
285 REM --------------------------------------------------
290 REM  MAIN LOOP
295 REM --------------------------------------------------
300 FOR A0 = 3510 TO 3501 STEP -1
305 B0 = B0 * 0.95 :REM LOYALTY SLOWLY DIMINISHES
310 GOSUB 625     :REM START REPORT
315 GOSUB 670 :REM CHECK POPULATION
320 GOSUB 725     :REM CHECK PLAGUE
325 GOSUB 760     :REM CHECK FAMINE
330 GOSUB 790        :REM CHECK WAR
335 GOSUB 825  :REM COMPLETE REPORT
340 ? DN$(49) HR$;
345 GOSUB 900 :REM BUY/SELL LAND
350 GOSUB 1000 :REM FEED PEOPLE
355 GOSUB 1070 :REM PLANT FIELDS
360 IF A0<3512 THEN GOSUB 1145 :REM WAGE WAR
365 ?: INPUT "   PRESS <RETURN>"; YN$
370 GOSUB 1190 :REM HANDLE ALL EVENTS
375 NEXT A0 :REM NEXT YEAR
380 REM --------------------------------------------------
385 REM  END GAME
390 REM --------------------------------------------------
395 ? CL$(10) SPC(30) "      END OF TERM"
400 ? HR$ "\X11"
405 P1 = INT(B3 * 100)
410 L  = INT(0.5 + A5 / A8)
415 ? SP$ "O GREAT HAMMURABI, THE YEAR IS"A0"BC\X11"
420 ? SP$ "IN YOUR 12-YEAR TERM OF OFFICE," P1 "% OF THE"
425 ? SP$ "PEOPLE STARVED EACH YEAR ON AVERAGE; THAT IS," B2 "PEOPLE STARVED!"
430 ?
435 ? SP$ "YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
440 ? SP$ L "ACRES PER PERSON."
445 IF P1>33 OR L<7  GOTO 465
450 IF P1>10 OR L<9  GOTO 470
455 IF P1>3  OR L<10 GOTO 475
460 ? SP$ "YOU WERE RECORDED AS A WISE AND JUST RULER!" :END
465 ? SP$ "YOU WERE RECORDED AS THE INCOMPETENT NATIONAL FINK!" :END
470 ? SP$ "YOU WERE RECORDED AS HEAVY-HANDED AND UNPOPULAR." :END 
475 ? SP$ "YOU WERE RECORDED AS A SO-SO RULER." :END
480 REM --------------------------------------------------
485 REM  INSTRUCTIONS
490 REM --------------------------------------------------
495 ? CL$(10)
500 READ A$
505 ? SP$ A$ "\X11"
510 IF A$ <> "" GOTO 500
515 DATA "THE YEAR IS 3500 BC.  YOU ARE THE RULER OF THE ANCIENT STATE OF"
520 DATA "SUMER. YOU HAVE TWELVE YEARS TO RUN YOUR EMPIRE."
525 DATA "\X11"
530 DATA "FEED EACH PERSON 20 BUSHELS OF GRAIN PER YEAR; LESS WILL DECREASE"
535 DATA "HEALTH AND LOYALTY, WHILE MORE WILL INCREASE BOTH. A HEALTHY "
540 DATA "POPULATION IS STRONGER AND GROWS FASTER."
545 DATA "\X11"
550 DATA "EACH PERSON CAN PLANT TWO ACRES OF FARMLAND WITH ONE BUSHEL OF GRAIN."
555 DATA "EACH ACRE YIELDS BETWEEN 2 AND 7 BUSHELS OF GRAIN. FAMINE REDUCES"
560 DATA "THE YIELD PER ACRE, AND RATS EAT SURPLUS STORED GRAIN."
565 DATA "\X11"
570 DATA "THE PRICE OF LAND AVERAGES 20 BUSHELS PER ACRE."
575 DATA "\X11"
580 DATA "WAR AND PLAGUE WILL KILL PEOPLE AND LOWER LOYALTY.
585 DATA "WINNING A BATTLE WILL INCREASE LOYALTY.  DO NOT START A FIGHT IF THE "
590 DATA "POPULATION IS IN POOR HEALTH."
595 DATA ""
600 ?:INPUT "     PRESS <RETURN> TO BEGIN"; YN$
605 RETURN
610 REM --------------------------------------------------
615 REM  START THE REPORT
620 REM --------------------------------------------------
625 ? CL$(4);
630 ? SP$ "O KING OF SUMER, I AM SORRY TO REPORT, ";
635 ? "IN YEAR" (3513 - A0);
640 ? "(" A0 "BC )"
645 ? HR$ "\X11"
650 RETURN
655 REM --------------------------------------------------
660 REM CHECK POPULATION
665 REM --------------------------------------------------
670 IF B1 > 0 THEN ? S2$ "** " B1 "NEW CITIZENS WERE BORN\X11"
675 IF B1 < 0 THEN ? S2$ "** " ABS(B1) "PEOPLE STARVED TO DEATH!\X11"
680 IF B1 < 0 THEN B2 = B2 - B1
685 X = 3513 - A0
690 IF B1 < 0 THEN B3 = B3 + (B1 / A8)/12
695 IF B4 > 0 THEN ? S2$ "** " B4 "PEOPLE MOVED TO SUMER\X11"
700 IF B4 < 0 THEN ? S2$ "** " B4 "PEOPLE LEFT SUMER\X11"
705 RETURN
710 REM --------------------------------------------------
715 REM CHECK PLAGUE
720 REM --------------------------------------------------
725 IF A1 = 0 THEN ? S2$ "THE STREETS ARE BEAUTIFUL\X11" :RETURN
730 ? S2$ "A PLAGUE STRUCK YOUR CITY!\X11"
735 ? S2$ "** " A1 "PEOPLE DIED\X11"
740 RETURN :REM PLAGUE DONE
745 REM --------------------------------------------------
750 REM CHECK FAMINE
755 REM --------------------------------------------------
760 IF A2 = 0 THEN ? S2$ "THE LAND IS HEALTHY\X11" :RETURN
765 ? S2$ "THERE IS FAMINE IN THE LAND!\X11" :RETURN 
770 RETURN :REM FAMINE DONE
775 REM --------------------------------------------------
780 REM CHECK WAR
785 REM --------------------------------------------------
790 IF A4 > 0 THEN ? S2$ "SUMER ATTACKS THE "AS$"!\X11":RETURN
795 IF A4 < 0 THEN ? S2$ "SUMER WAS ATTACKED!\X11" :RETURN
800 IF A3>0 THEN?S2$"YOU ARE AT WAR WITH THE "AS$"!\X11" :RETURN
805 ? S2$ "SUMER IS AT PEACE.\X11" :RETURN
810 REM --------------------------------------------------
815 REM  COMPLETE THE REPORT
820 REM --------------------------------------------------
825 ? 
830 ? S2$ "SUMER HAS" A8 "PEOPLE;\X11"
835 ? S2$ "IT HAS"A5"ACRES OF FARMLAND;"A6"WERE PLANTED.\X11"
840 REM RATS?
845 IF B8>0 THEN ? S2$ "RATS ATE"B8"BUSHELS OF STORED GRAIN.\X11"
850 REM SPOILAGE?
855 IF B9>0 THEN ? S2$ "\X9D" B9"BUSHELS OF STORED GRAIN SPOILED.\X11"
860 ? S2$ "THE HARVEST WAS" B6 "BUSHELS PER ACRE,\X11"
865 T = B6 * A6
870 ? SPC(25) "FOR A TOTAL OF" T "BUSHELS.\X11"
875 ? S2$ "\X11THE ROYAL GRANARIES NOW HOLD" B7 "BUSHELS OF GRAIN.\X11"
880 RETURN :REM COMPLETE REPORT
885 REM --------------------------------------------------
890 REM  BUY AND SELL LAND
895 REM --------------------------------------------------
900 LL = INT(RND(1)*10+16)
905 IF B7 < LL GOTO 945
910 ? DN$(48);EL$;DN$(48);
915 ?"  LAND IS"LL"BUSHELS PER ACRE. YOU HAVE"A5"ACRES AND"B7 "BUSHELS."
920 ? DN$(50);EL$;DN$(50);
925 INPUT "  HOW MANY ACRES WILL YOU BUY";AB$
930 AB=VAL(AB$) :IF AB*LL>B7 THEN 910
935 B7 = B7 - AB*LL
940 A5 = A5 + AB
945 ? DN$(48);EL$;DN$(48);
950 ?"  LAND IS"LL"BUSHELS PER ACRE. YOU HAVE"A5"ACRES AND"B7 "BUSHELS."
955 ? DN$(50);EL$;DN$(50);
960 INPUT "  HOW MANY ACRES WILL YOU SELL";AB$
965 AB=VAL(AB$) :IF AB>A5 THEN 945
970 B7 = B7 + AB*LL
975 A5 = A5 - AB
980 RETURN :REM BUY AND SELL LAND
985 REM --------------------------------------------------
990 REM  FEED YOUR PEOPLE
995 REM --------------------------------------------------
1000 ? DN$(48);EL$;DN$(48);
1005 ? "  YOU HAVE"A5"ACRES AND"B7 "BUSHELS."
1010 ? DN$(50); EL$ ; DN$(50); 
1015 INPUT "  HOW MANY BUSHELS WILL YOU USE TO FEED YOUR PEOPLE"; BU$
1020 BU = VAL(BU$) :IF BU>B7 GOTO 1000
1025 B7 = B7 - BU
1030 BU = (BU / A8) - 20
1035 B0 = B0 + BU/50
1040 A9 = A9 + BU/100
1045 ? DN$(59) "LOYALTY:" B0 ", HEALTH:" A9 ", BU/POP:" BU / A8;
1050 RETURN
1055 REM --------------------------------------------------
1060 REM  PLANT
1065 REM --------------------------------------------------
1070 ? DN$(48);EL$;DN$(48);
1075 ? "  YOU HAVE"A5"ACRES AND"B7 "BUSHELS."
1080 ? DN$(50);EL$;DN$(50);
1085 INPUT "  HOW MANY ACRES WILL YOU PLANT"; PA$
1090 PA = VAL(PA$)
1095 IF PA>B7 * 2 THEN ?:? "  YOU LACK THE GRAIN" :GOTO 1070
1100 IF PA>A5 THEN ?:? "  YOU LACK THE LAND" :GOTO 1070
1105 IF PA>A8 * 10 THEN ?:? "  YOU DON'T HAVE ENOUGH PEOPLE" :GOTO 1070
1110 ? DN$(52);EL$
1115 B7 = B7 - INT(PA/2)
1120 A6 = PA 
1125 RETURN :REM PLANT
1130 REM --------------------------------------------------
1135 REM WAGE WAR ON THE ACCURSED SEA PEOPLES
1140 REM --------------------------------------------------
1145 IF A3 > 0 THEN RETURN
1150 ? DN$(50); EL$ ; DN$(50);
1155 INPUT "  DO YOU WISH TO WAGE WAR AGAINST THE ACCURSED SEA PEOPLES"; YN$
1160 A4 = 0
1165 YN=VAL(YN$) :IF YN$="Y" THEN A4 = 1
1170 RETURN
1175 REM --------------------------------------------------
1180 REM HANDLE ALL EVENTS
1185 REM --------------------------------------------------
1190 REM
1195 REM GRAIN EATEN = RAT POPULATION
1200 REM RAT POPULATION WILL GROW IF YOU KEEP
1205 REM A LARGE SURPLUS OVER MULTIPLE YEARS
1210 REM
1215 B8 = B8 + INT(RND(1)*B7 /100)*10
1220 IF B8 > B7  THEN B8 = B7 
1225 B7 = B7 - B8
1230 REM
1235 REM GRAIN ROT IS NOT CUMULATIVE, I GUESS
1240 REM
1245 B9 = INT(RND(1)*B7 /30)*10
1250 IF B9 > B7 THEN B9 = B7
1255 B7 = B7 - B9
1260 REM
1265 REM GRAIN HARVEST
1270 REM
1275 B6 = INT(RND(1)*4+4) - A2 :REM 3 TO 7 BU/ACRE NORMALLY
1280 IF B6 < 2 THEN B6 = 2
1285 B7 = B7 + B6 * A6
1290 REM
1295 REM BIRTHS
1300 REM
1305 B1 = INT( (A9-0.5) * A8 * RND(1)/8 )
1310 A8 = A8 + B1
1315 REM
1320 REM IMMIGRATION
1325 REM
1330 B4 = INT( (B0-0.5) * A8 * RND(1)/5 )
1335 A8 = A8 + B4
1340 REM
1345 REM START/END PLAGUE
1350 REM
1355 A1 = 0
1360 IF RND(1)>0.7 THEN A1 = INT(A8 * RND(1) * 0.5) 
1365 IF A1>0 THEN A9 = A9 * 0.9 :REM ERODE HEALTH
1370 A8 = A8 - A1
1375 REM
1380 REM START/END FAMINE
1385 REM
1390 IF RND(1)>0.7 THEN A2 = INT(RND(1)*4+1) 
1395 IF RND(1)<0.2 THEN A2 = 0
1400 REM
1405 REM WAGE WAR
1410 REM
1415 IF A3 = 0 GOTO 1470
1420 B0 = B0 * 1.05 :REM LOYALTY INCREASES DURING WAR
1425 A8 = A8 - INT(RND(1)*A8/20)
1430 IF RND(1)>0.5 THEN A3 = 0 
1435 REM
1440 REM IF SUMER APPEARS WEAK, THEN THE SEA PEOPLES ATTACK
1445 REM
1450 IF RND(1) > 0.5 GOTO 1470
1455 IF A2 == 0 AND A1 == 0 THEN RETURN
1460 B0 = B0 * 0.95 :REM LOYALTY DECREASES DUE TO INCURSION
1465 A4 = -1
1470 RETURN
