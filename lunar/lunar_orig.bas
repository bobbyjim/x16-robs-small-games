10 	SC = 32 :REM SCREEN INDENT
20 	COLOR 5,0 : ? CHR$(147) "\X11"
30 	? TAB(SC) "LUNAR"
40 	? TAB(SC/2) "\X11\X11CREATIVE COMPUTING   MORRISTOWN, NEW JERSEY"
50 	? "\X11\X11\X11\X11THIS IS A COMPUTER SIMULATION OF AN APOLLO LUNAR"
60 	? "LANDING CAPSULE."
70 	? "\X11\X11\X11THE ON-BOARD COMPUTER HAS FAILED (IT WAS MADE BY"
80 	? "XEROX) SO YOU HAVE TO LAND THE CAPSULE MANUALLY.\X11"
90 	? "SET BURN RATE OF RETRO ROCKETS TO ANY VALUE BETWEEN"
100 	? "0 (FREE FALL) AND 200 (MAXIMUM BURN) POUNDS PER SECOND."
110 	? "SET NEW BURN RATE EVERY 10 SECONDS."
120         M=33000 :N=16500
130 	? "\X11PLEASE SELECT THE LANDER'S TECHNOLOGY:"
140 	? "   1 - 1970 (HARDER)"
150 	? "   2 - 1990 (DEFAULT)"
160 	? "   3 - 2010 (EASIER)"
170 	INPUT TL
180 	IF TL = 1 THEN M=32500 :N=16000
190 	IF TL = 2 THEN M=33000 :N=16500
200 	IF TL = 3 THEN M=30000 :N=10000
210 	? "\X11CAPSULE WEIGHT" M "LBS; FUEL WEIGHT" M-N "LBS."
220 	? "\X11\X11\X11GOOD LUCK"
230 	L%=0 :REM SECONDS ELAPSED
240 	? "\X11 SEC", " KM", " M/S", "LB FUEL", "BURN RATE\X11"
250 	A=120 :V=1 :G=.001 :Z=1.8
260         KM$ = MID$(STR$(1.6*A)+"     ",1,6)
270         MS  = INT(100000*V)
280 	? L%, KM$, MS,M-N,:INPUT K :T=10
290 	IF M-N<.001 GOTO 370
300 	IF T<.001   GOTO 260
310 	S=T :IF M>=N+S*K GOTO 330
320 	S=(M-N)/K
330 	GOSUB 590 :IF I<=0 GOTO 500
340 	IF V<=0 GOTO 360	
350 	IF J<0 GOTO 530
360 	GOSUB 490 :GOTO 290
370 	? "\X11FUEL OUT AT" L% "SECONDS" :S=(-V+SQR(V*V+2*A*G))/G
380 	V=V+G*S :L%=L%+S
390 	W=1.6*3600*V :? "\X11ON MOON AT" L% "SECONDS - IMPACT VELOCITY" INT(W) "KPH"
400 	IF W<=1.2 THEN ? "PERFECT LANDING!" :GOTO 620
410 	IF W<=10  THEN ? "GOOD LANDING (COULD BE BETTER)" :GOTO 620
420 	IF W>60 THEN 460
430 	? "\X11CRAFT DAMAGE... YOU'RE STRANDED HERE UNTIL A RESCUE"
440 	? "PARTY ARRIVES.  HOPE YOU HAVE ENOUGH OXYGEN!"
450 	GOTO 620
460 	? "\X11SORRY THERE WERE NO SURVIVORS.  YOU BLEW IT!"
470 	? "IN FACT, YOU BLASTED A NEW LUNAR CRATER" W*.052768 "METERS DEEP!"
480 	GOTO 620
490 	L%=L%+S :T=T-S :M=M-S*K :A=I :V=J :RETURN
500 	IF S<.005 GOTO 390
510 	D = V + SQR(V*V+2*A*ABS(G-Z*K/M)) :S=2*A/D
520 	GOSUB 590 :GOSUB 490 :GOTO 500
530 	W=(1-M*G/(Z*K))/2 :S=M*V/(Z*K*(W+SQR(W*W+V/Z)))+0.5 :GOSUB 590
540 	REM W=(1-M*G/(Z*K))/2 :S=M*V/(Z*K*(W+SQR(W*Q+V/Z)))+0.5 :GOSUB 590
550 	IF I<=0 GOTO 500
560 	GOSUB 490 :IF J>0 GOTO 290
570 	IF V>0 GOTO 530
580 	GOTO 290
590 	Q=S*K/M :J=V+G*S+Z*(-Q-Q*Q/2-Q^3/3-Q^4/4-Q^5/5)
600 	I=A-G*S*S/2 - V*S + Z*Z*(Q/2 + Q*Q/6 + Q^3/12 + Q^4/20 + Q^5/30)
610 	RETURN
620 	? "\X11\X11\X11TRY AGAIN??\X11" :GOTO 90
